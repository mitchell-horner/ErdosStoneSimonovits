import Mathlib
import ErdosStoneSimonovits.Combinatorics.SimpleGraph.DeleteEdges
import ErdosStoneSimonovits.Combinatorics.SimpleGraph.Extremal.Basic

open Asymptotics Filter Finset Fintype Topology

namespace SimpleGraph

variable {V W : Type*} {G : SimpleGraph V} {H : SimpleGraph W}

section TuranDensity

/-- If `G` is `H.Free`, then `G.deleteIncidenceSet v` is also `H.Free` and has at most
`extremalNumber { x // x ‚â† v } H` many edges. -/
theorem card_deleteIncidenceSet_le_extremalNumber_of_free
    [Fintype V] [DecidableRel G.Adj] [DecidableEq V] (h : H.Free G) (v : V) :
    #(G.deleteIncidenceSet v).edgeFinset ‚â§ extremalNumber { x // x ‚â† v } H := by
  let e : ({v}·∂ú : Set V) ‚âÉ { x // x ‚â† v } := by
    simpa [Set.Elem, Set.mem_compl_iff, Set.not_mem_singleton_iff]
      using (Equiv.refl { x // x ‚â† v })
  rw [‚Üê card_edgeFinset_induce_compl_singleton, ‚Üê extremalNumber_congr e Iso.refl]
  apply le_extremalNumber
  contrapose! h
  rw [not_free] at h ‚ä¢
  exact h.trans ‚ü®Copy.induce G {v}·∂ú‚ü©

lemma extremalNumber_div_choose_two_succ_le {n : ‚Ñï} (hn : 2 ‚â§ n) :
    (extremalNumber (Fin (n+1)) H / (n+1).choose 2 : ‚Ñù)
      ‚â§ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù) := by
  rw [div_le_iff‚ÇÄ (mod_cast Nat.choose_pos (by linarith)),
    extremalNumber_le_iff_of_nonneg (Fin (n+1)) H (by positivity)]
  intro G _ h
  rw [mul_comm, ‚Üê mul_div_assoc, le_div_iff‚ÇÄ' (mod_cast Nat.choose_pos hn)]
  suffices #G.edgeFinset * (n-1) ‚â§ extremalNumber (Fin n) H * (n+1) by
    rw [mul_comm (n.choose 2 : ‚Ñù) _, Nat.cast_choose_two, ‚Üê mul_div_assoc,
      mul_comm ((n+1).choose 2 : ‚Ñù) _, Nat.cast_choose_two, ‚Üê mul_div_assoc,
      div_le_div_iff_of_pos_right zero_lt_two, ‚Üê Nat.cast_pred (by positivity),
      ‚Üê Nat.cast_pred (by positivity), mul_comm (n : ‚Ñù) _, ‚Üê mul_assoc, ‚Üê mul_assoc,
      add_tsub_cancel_right n 1, mul_le_mul_right (by positivity)]
    assumption_mod_cast
  -- double-counting v ‚àâ e
  let s := (univ √óÀ¢ G.edgeFinset).filter fun (v, e) ‚Ü¶ v ‚àâ e
  trans #s
  -- counting e
  ¬∑ conv_rhs =>
      rw [card_filter, sum_product_right]
      enter [2, y]
      rw [‚Üê card_filter]
    simp_rw [Sym2.mem_toFinset, filter_not, filter_mem_eq_inter, ‚Üê sum_attach G.edgeFinset,
      univ_inter, ‚Üê compl_eq_univ_sdiff, card_compl, Fintype.card_fin, card_toFinset_mem_edgeFinset,
      sum_const, card_attach, smul_eq_mul, Nat.succ_sub_succ_eq_sub, le_refl]
  -- counting v
  ¬∑ conv_rhs =>
      rw [‚Üê Fintype.card_fin (n+1), ‚Üê card_univ, mul_comm, ‚Üê smul_eq_mul, ‚Üê sum_const]
    conv_lhs =>
      rw [card_filter, sum_product]
      enter [2, x]
      rw [‚Üê card_filter]
    apply sum_le_sum
    intro i _
    let e : { x // x ‚â† i } ‚âÉ Fin n := Fintype.equivFinOfCardEq (by simp)
    rw [‚Üê edgeFinset_deleteIncidenceSet_eq_filter, extremalNumber_congr e.symm Iso.refl]
    exact card_deleteIncidenceSet_le_extremalNumber_of_free h i

/-- The limit `extremalNumber (Fin n) H / n.choose 2` as `n` approaches `‚àû` exists. -/
lemma exists_tendsto_extremalNumber_div_choose_two (H : SimpleGraph V) :
    ‚àÉ x, Tendsto (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù)) atTop (ùìù x) := by
  let f := fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù)
  suffices h : ‚àÉ x, Tendsto (fun (n : ‚Ñï) ‚Ü¶ f (n + 2)) atTop (ùìù x) by
    simpa [tendsto_add_atTop_iff_nat 2] using h
  -- limit of antitone sequence bounded from below is infimum
  use ‚®Ö n, f (n + 2)
  apply tendsto_atTop_ciInf
  ¬∑ apply antitone_nat_of_succ_le
    intro n
    apply extremalNumber_div_choose_two_succ_le
    rw [le_add_iff_nonneg_left]
    exact Nat.zero_le n
  ¬∑ simp_rw [bddBelow_def, Set.mem_range, forall_exists_index, forall_apply_eq_imp_iff]
    use 0
    intro n
    positivity

/-- The **Tur√°n density** of a simple graph `H` is the limit of
`extremalNumber (Fin n) H / n.choose 2` as `n` approaches `‚àû`.

See `SimpleGraph.tendsto_turanDensity` for proof of existence. -/
noncomputable def turanDensity (H : SimpleGraph V) :=
  limUnder atTop fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù)

/-- The **Tur√°n density** of a simple graph `H` is well-defined. -/
theorem tendsto_turanDensity (H : SimpleGraph V) :
    Tendsto (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù))
      atTop (ùìù (turanDensity H)) := by
  have ‚ü®_, h‚ü© := exists_tendsto_extremalNumber_div_choose_two H
  rwa [‚Üê Tendsto.limUnder_eq h] at h

/-- `extremalNumber (Fin n) H` is asymptotically equivalent to `turanDensity H * n.choose 2` as
`n` approaches `‚àû`. -/
theorem isEquivalent_extremalNumber {H : SimpleGraph V} (h : turanDensity H ‚â† 0) :
    (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H : ‚Ñù))
      ~[atTop] (fun (n : ‚Ñï) ‚Ü¶ (turanDensity H * n.choose 2 : ‚Ñù)) := by
  have hz : ‚àÄ·∂† (x : ‚Ñï) in atTop, turanDensity H * x.choose 2 ‚â† 0 := by
    rw [eventually_atTop]
    use 2; intro n hn
    simp [h, Nat.choose_eq_zero_iff, hn]
  rw [isEquivalent_iff_tendsto_one hz]
  have hœÄ := tendsto_turanDensity H
  apply Tendsto.const_mul (1 / (turanDensity H) : ‚Ñù) at hœÄ
  simp_rw [one_div_mul_cancel h] at hœÄ
  convert hœÄ
  field_simp [Pi.div_apply]

end TuranDensity

end SimpleGraph
