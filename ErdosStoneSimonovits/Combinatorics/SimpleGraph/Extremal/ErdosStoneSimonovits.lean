import Mathlib
import ErdosStoneSimonovits.Analysis.SpecialFunctions.Choose
import ErdosStoneSimonovits.Combinatorics.SimpleGraph.Bipartite
import ErdosStoneSimonovits.Combinatorics.SimpleGraph.Equipartite
import ErdosStoneSimonovits.Combinatorics.SimpleGraph.Extremal.TuranDensity
import ErdosStoneSimonovits.Data.Finset.Union
import ErdosStoneSimonovits.Data.Nat.Cast.Order.Field
import ErdosStoneSimonovits.Data.Nat.Defs

open Asymptotics Filter Finset Fintype Real Topology

namespace SimpleGraph

section ErdosStone

variable {V : Type*} [Fintype V] {G : SimpleGraph V} [DecidableEq V] [DecidableRel G.Adj]
  {Œµ : ‚Ñù} {r t t' : ‚Ñï} (A : G.completeEquipartiteSubgraph r t')

local notation "K" => A.verts

/-- `W` is the set of vertices in the complement of a complete equipartite subgraph,
in `r` parts each of size `t'`, adjacent to at least `t` vertices in each part of the complete
equipartite subgraph.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private abbrev aux (t : ‚Ñï) : Finset V :=
  { v ‚àà K·∂ú | ‚àÄ i : Fin r, ‚àÉ s : (A.parts i).val.powersetCard t, ‚àÄ w ‚àà s.val, G.Adj v w }

local notation "W" => aux A t

private lemma aux_subset_verts_compl : W ‚äÜ K·∂ú := by apply filter_subset

omit [DecidableRel G.Adj] in
private lemma between_verts_isBipartiteWith : (G.between K K·∂ú).IsBipartiteWith K ‚ÜëK·∂ú := by
  rw [coe_compl K]
  exact between_isBipartiteWith (disjoint_compl_right)

private lemma le_card_edgeFinset_between_verts :
    (#K*(G.minDegree-#K) : ‚Ñù) ‚â§ #(G.between K K·∂ú).edgeFinset := by
  rw [‚Üê isBipartiteWith_sum_degrees_eq_card_edges (between_verts_isBipartiteWith A),
    ‚Üê nsmul_eq_mul, ‚Üê sum_const, Nat.cast_sum]
  apply sum_le_sum; intro v hv
  rw [sub_le_iff_le_add]
  exact_mod_cast (minDegree_le_degree G v).trans (degree_le_between_plus hv)

/-- For `v ‚àà K·∂ú\W`, since `v` is adjacent to fewer than `t` vertices in at least one part of the
complete equipartite subgraph, it follows that `v` is adjacent to fewer than `#K-(t'-t)` vertices
in `K`.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private lemma degree_between_verts_lt_of_mem_sdiff {v : V} (hv : v ‚àà K·∂ú\W) :
    (G.between K K·∂ú).degree v < #K-t'+t := by
  rw [mem_sdiff, mem_filter] at hv
  push_neg at hv
  have ‚ü®i, hs‚ü© := hv.2 hv.1
  rw [‚Üê card_neighborFinset_eq_degree,
    isBipartiteWith_neighborFinset' (between_verts_isBipartiteWith A) hv.1,
    filter_disjiUnion, card_disjiUnion, ‚Üê union_compl {i}, union_comm,
    sum_union disjoint_compl_left, sum_singleton]
  apply add_lt_add_of_le_of_lt
  ¬∑ conv_rhs =>
      rw [A.card_verts, ‚Üê Nat.sub_one_mul,  ‚Üê Fintype.card_fin r, ‚Üê card_singleton i,
        ‚Üê card_compl, ‚Üê smul_eq_mul, ‚Üê sum_const]
      enter [2, j]
      rw [‚Üê A.card_parts j]
    apply sum_le_sum
    intros
    apply card_filter_le
  ¬∑ contrapose! hs
    obtain ‚ü®s, hs‚ü© := by
      rwa [‚Üê powersetCard_nonempty] at hs
    have hs' : s ‚àà  (A.parts i).val.powersetCard t := by
      apply powersetCard_mono _ hs
      exact filter_subset _ (A.parts i).val
    use ‚ü®s, hs'‚ü©
    intro w hw
    obtain ‚ü®_, hadj, _‚ü© := by
      rw [mem_powersetCard] at hs
      apply hs.1 at hw
      rwa [mem_filter, between_adj] at hw
    exact hadj.symm

private lemma card_edgeFinset_between_verts_le (hr : 0 < r) :
    (#(G.between K K·∂ú).edgeFinset : ‚Ñù) ‚â§ ((card V)-#K)*(#K-(t'-t)) + #W*(t'-t) :=
  calc (#(G.between K K·∂ú).edgeFinset : ‚Ñù)
    _ = ‚àë v ‚àà K·∂ú\W, ((G.between K K·∂ú).degree v : ‚Ñù)
          + ‚àë v ‚àà W, ((G.between K K·∂ú).degree v : ‚Ñù) := by
        rw [sum_sdiff (filter_subset _ K·∂ú), eq_comm]
        exact_mod_cast isBipartiteWith_sum_degrees_eq_card_edges' (between_verts_isBipartiteWith A)
    _ ‚â§ ‚àë _ ‚àà K·∂ú\W, (#K-t'+t : ‚Ñù) + ‚àë _ ‚àà W, (#K : ‚Ñù) := by
        apply add_le_add
        ¬∑ apply sum_le_sum; intro v hv
          rw [‚Üê Nat.cast_sub]
          ¬∑ exact_mod_cast (degree_between_verts_lt_of_mem_sdiff A hv).le
          ¬∑ rw [A.card_verts]
            apply Nat.le_mul_of_pos_left t' hr
        ¬∑ apply sum_le_sum; intro v hv
          exact_mod_cast isBipartiteWith_degree_le'
            (between_verts_isBipartiteWith A) (aux_subset_verts_compl A hv)
    _ = ((card V)-#K)*(#K-(t'-t)) + #W*(t'-t) := by
        simp_rw [sum_const, card_sdiff (aux_subset_verts_compl A), nsmul_eq_mul,
          Nat.cast_sub (card_le_card (aux_subset_verts_compl A)), card_compl,
          Nat.cast_sub (card_le_univ K)]
        ring_nf

private lemma card_aux_ge (hr : 0 < r) :
    (#K*(G.minDegree-#K)-((card V)-#K)*(#K-(t'-t)) : ‚Ñù) ‚â§ (#W*(t'-t) : ‚Ñù) :=
  sub_left_le_of_le_add <| (le_card_edgeFinset_between_verts A).trans
    (card_edgeFinset_between_verts_le A hr)

/-- `#W` is arbitrarily large with respect to `card V`.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private lemma card_aux_ge_of_minDegree (hr : 0 < r)
    (hŒ¥ : G.minDegree ‚â• (1-1/r+Œµ)*(card V))
    {x : ‚Ñï} (hx : (x+r*t')*(t'-t) ‚â§ (card V)*(t'*r*Œµ-t)) :
    (x*(t'-t) : ‚Ñù) ‚â§ (#W*(t'-t) : ‚Ñù) := by
  calc (x*(t'-t) : ‚Ñù)
    _ ‚â§ (card V)*(t'*r*Œµ-t)-r*t'*(t'-t) := by
        rw [‚Üê add_sub_cancel_right (x : ‚Ñù) (r*t' : ‚Ñù), sub_mul]
        apply sub_le_sub_right hx
    _ = #K*((1-1/r+Œµ)*(card V)-#K)-((card V)-#K)*(#K-(t'-t)) := by
        rw [A.card_verts]
        field_simp
        ring_nf
    _ ‚â§ #K*(G.minDegree-#K)-((card V)-#K)*(#K-(t'-t)) := by
        apply sub_le_sub_right
        apply mul_le_mul_of_nonneg_left _ (#K).cast_nonneg
        apply sub_le_sub_right hŒ¥
    _ ‚â§ #W*(t'-t) := by
        apply sub_left_le_of_le_add
        exact (le_card_edgeFinset_between_verts A).trans
          (card_edgeFinset_between_verts_le A hr)

/-- For `w ‚àà W`, there exists a `r` subets of vertices of size `t < t'` adjacent to `w`.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private noncomputable abbrev aux.Pi :
    W ‚Üí Œ† i : Fin r, powersetCard t (A.parts i).val := by
  intro ‚ü®w, hw‚ü© i
  rw [mem_filter] at hw
  exact (hw.2 i).choose

local notation "F" => aux.Pi A

private lemma aux.Pi_mem_val (w : W) (i : Fin r) :
    ‚àÄ v ‚àà (F w i).val, G.Adj w v := by
  have hw := w.prop
  rw [mem_filter] at hw
  exact (hw.2 i).choose_spec

/-- For `y = F ¬∑`, there exists a complete equipartite subgraph in `r` parts of size `t < t'`.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private noncomputable abbrev aux.completeEquipartiteSubgraph
    (y : Œ† i : Fin r, powersetCard t (A.parts i).val) :
    G.completeEquipartiteSubgraph r t where
  parts i := by
    use (y i).val
    have hyi := mem_powersetCard.mp (y i).prop
    simpa using hyi.2
  Adj i j h v hv w hw := by
    have hyi := mem_powersetCard.mp (y i).prop
    have hyj := mem_powersetCard.mp (y j).prop
    exact A.Adj h v (hyi.1 hv) w (hyj.1 hw)

include A in
/-- If `#W` is sufficently large, then there exist at least `t` vertices adjacent to the vertices
of a complete equipartite subgraph in `r` parts each of size `t < t'`.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private lemma exists_completeEquipartiteSubgraph_powersetCard (hr : 0 < r)
    (ht_lt_t' : t < t') (hŒ¥ : G.minDegree ‚â• (1-1/r+Œµ)*(card V))
    (hx : ((t'.choose t)^r*t+r*t')*(t'-t) ‚â§ (card V)*(t'*r*Œµ-t)) :
    ‚àÉ (A : G.completeEquipartiteSubgraph r t) (s : univ.powersetCard t),
      ‚àÄ v‚ÇÅ ‚àà s.val, ‚àÄ i, ‚àÄ v‚ÇÇ ‚àà (A.parts i).val, G.Adj v‚ÇÅ v‚ÇÇ := by
  have ht_sub_t'_pos :  0 < (t'-t : ‚Ñù) := sub_pos_of_lt (mod_cast ht_lt_t')
  have ‚ü®y, hy‚ü© : ‚àÉ y : Œ† i : Fin r, powersetCard t (A.parts i).val,
      t ‚â§ #(univ.filter (F ¬∑ = y)) := by
    haveI : Nonempty (Œ† i : Fin r, powersetCard t (A.parts i).val) := by
      simpa only [Fintype.card_fin, Classical.nonempty_pi, nonempty_coe_sort,
        powersetCard_nonempty, A.card_parts] using Function.const (Fin r) ht_lt_t'.le
    apply exists_le_card_fiber_of_mul_le_card
    simp_rw [Fintype.card_pi, card_coe, card_powersetCard,
      A.card_parts, prod_const, card_univ, Fintype.card_fin]
    rw [‚Üê @Nat.cast_le ‚Ñù, ‚Üê mul_le_mul_right ht_sub_t'_pos]
    apply card_aux_ge_of_minDegree A hr hŒ¥ (mod_cast hx)
  have ‚ü®s', hs'‚ü© := exists_subset_card_eq hy
  let s : univ.powersetCard t := by
    use s'.map (Function.Embedding.subtype (¬∑ ‚àà W))
    simpa using hs'.2
  use aux.completeEquipartiteSubgraph A y, s
  intro v hv i w hw
  let v' : W := ‚ü®v, property_of_mem_map_subtype s' hv‚ü©
  have hv' : v' ‚àà s' := by
    rw [Finset.mem_map] at hv
    convert hv.choose_spec.1
    rw [Subtype.ext_iff]
    exact hv.choose_spec.2.symm
  apply aux.Pi_mem_val A v' i w
  replace hv' := hs'.1 hv'
  rw [mem_filter] at hv'
  rw [hv'.2]
  exact hw

/-- If `G` has a minimal degree of at least `(1-1/r+o(1))*(card V)`, then `G` contains a copy of
a `completeEquipartiteGraph` in `r+1` parts each of size `t`.

This is the minimal-degree version of the **Erd≈ës-Stone theorem**. -/
theorem completeEquipartiteGraph_isContained_of_minDegree {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (r t : ‚Ñï) :
    ‚àÉ n, ‚àÄ {V : Type*} [Fintype V] [DecidableEq V], n < card V ‚Üí
      ‚àÄ {G : SimpleGraph V} [DecidableRel G.Adj],
        G.minDegree ‚â• (1-1/r+Œµ)*(card V)
          ‚Üí completeEquipartiteGraph (r+1) t ‚äë G := by
  rcases show (r = 0 ‚à® t = 0) ‚à® r ‚â† 0 ‚àß t ‚â† 0 by tauto with h | ‚ü®hr, ht‚ü©
  ¬∑ rw [‚Üê Nat.le_zero_eq, ‚Üê @Nat.add_le_add_iff_right r 0 1, zero_add] at h
    use (r+1)*t
    intro _ _ _ h_card _ _ _
    rw [completeEquipartiteGraph_eq_bot_iff.mpr h, bot_isContained_iff_card_le]
    simpa using h_card.le
  ¬∑ rw [‚Üê Nat.pos_iff_ne_zero] at hr ht
    -- choose `Œµ'` to ensure `Œ¥(G)` is large enough
    let Œµ' := 1/(‚Üë(r-1)*r) + Œµ
    have hŒµ' : 0 < Œµ' := by positivity
    -- choose `t'` larger than `t/(r*Œµ)`
    let t' := ‚åät/(r*Œµ)‚åã‚Çä + 1
    have ht_lt_t'rŒµ : t < t'*r*Œµ := by
      rw [mul_assoc, ‚Üê div_lt_iff‚ÇÄ (by positivity), Nat.cast_add_one]
      exact Nat.lt_floor_add_one (t/(r*Œµ))
    have ht' : 0 < t' := by positivity
    haveI : NeZero t' := ‚ü®ht'.ne'‚ü©
    have ‚ü®n', ih‚ü© := completeEquipartiteGraph_isContained_of_minDegree hŒµ' (r-1) t'
    -- choose `n` at least `((t'.choose t)^r*t+r*t')*(t'-t)/(t'*r*Œµ-t)` for pigeonhole principle
    let n := max n' ‚åà((t'.choose t)^r*t+r*t')*(t'-t)/(t'*r*Œµ-t)‚åâ‚Çä
    use n
    intro V _ _ h_cardV G _ hŒ¥
    haveI : Nonempty V := card_pos_iff.mp (n.zero_le.trans_lt h_cardV)
    -- `r` is less than `1/Œµ` otherwise Œ¥(G) = v(G)
    have hrŒµ_lt_1 : r*Œµ < 1 := by
      have hŒ¥_lt_card : (G.minDegree : ‚Ñù) < (card V : ‚Ñù) := mod_cast G.minDegree_lt_card
      contrapose! hŒ¥_lt_card with h1_le_rŒµ
      rw [‚Üê div_le_iff‚ÇÄ' (by positivity), ‚Üê sub_nonpos,
        ‚Üê le_sub_self_iff 1, ‚Üê sub_add] at h1_le_rŒµ
      exact hŒ¥.trans' (le_mul_of_one_le_left (card V).cast_nonneg h1_le_rŒµ)
    have ht_lt_t' : t < t' := by
      rw [‚Üê @Nat.cast_lt ‚Ñù]
      apply ht_lt_t'rŒµ.trans
      rw [mul_assoc]
      exact mul_lt_of_lt_one_right (mod_cast ht') hrŒµ_lt_1
    -- find `completeEquipartiteGraph` from inductive hypothesis
    replace ih : completeEquipartiteGraph r t' ‚äë G := by
      rcases eq_or_ne r 1 with hr_eq_1 | hr_ne_1
      ¬∑ rw [completeEquipartiteGraph_eq_bot_iff.mpr (Or.inl hr_eq_1.le),
          bot_isContained_iff_card_le]
        simp_rw [card_prod, Fintype.card_fin, hr_eq_1, one_mul]
        apply h_cardV.le.trans'
        exact_mod_cast calc (t' : ‚Ñù)
        _ ‚â§ r*t' := le_mul_of_one_le_left (by positivity) (mod_cast hr)
        _ ‚â§ (t'.choose t)^r*t + r*t' := le_add_of_nonneg_left (by positivity)
        _ ‚â§ ((t'.choose t)^r*t + r*t')*(t'-t)/(t'*r*Œµ-t) := by
            rw [mul_div_assoc, le_mul_iff_one_le_right (by positivity),
              one_le_div (sub_pos.mpr ht_lt_t'rŒµ), sub_le_sub_iff_right, mul_assoc,
              mul_le_iff_le_one_right (by positivity)]
            exact hrŒµ_lt_1.le
        _ ‚â§ ‚åà((t'.choose t)^r*t + r*t')*(t'-t)/(t'*r*Œµ-t)‚åâ‚Çä := Nat.le_ceil _
        _ ‚â§ n := Nat.cast_le.mpr (le_max_right n' _)
      ¬∑ have hŒ¥' := calc (G.minDegree : ‚Ñù)
          _ ‚â• (1-1/(r-1)+(1/(r-1)-1/r)+Œµ)*(card V) := by rwa [‚Üê sub_add_sub_cancel] at hŒ¥
          _ = (1-1/‚Üë(r-1)+Œµ')*(card V) := by
              rw [‚Üê one_div_mul_sub_mul_one_div_eq_one_div_add_one_div
                  (sub_ne_zero.mpr (mod_cast hr_ne_1)) (mod_cast hr.ne'), sub_sub_cancel, mul_one,
                one_div_mul_one_div_rev, mul_comm (r : ‚Ñù), ‚Üê Nat.cast_pred hr, add_assoc]
        rw [‚Üê Nat.succ_pred_eq_of_pos hr]
        exact ih (h_cardV.trans_le' (le_max_left n' _)) hŒ¥'
    obtain ‚ü®A‚ü© := by rwa [completeEquipartiteGraph_isContained_iff] at ih
    -- find `completeEquipartiteGraph` from pigeonhole principle
    rw [completeEquipartiteGraph_succ_isContained_iff]
    obtain ‚ü®A', s, hs‚ü© := by
      apply exists_completeEquipartiteSubgraph_powersetCard A hr ht_lt_t' hŒ¥
      rw [‚Üê div_le_iff‚ÇÄ (sub_pos.mpr ht_lt_t'rŒµ)]
      trans (n : ‚Ñù)
      ¬∑ apply (Nat.le_ceil _).trans <| Nat.cast_le.mpr (le_max_right n' _)
      ¬∑ exact_mod_cast h_cardV.le
    use A', s, hs

/-- Repeatedly remove minimal degree vertices until `(G.induce s).minDegree` is
at least `c * #s`.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private lemma exists_induced_subgraph_for_minDegree {c : ‚Ñù} (hc‚ÇÄ : 0 ‚â§ c) (hc‚ÇÅ : c ‚â§ 1)
    (G : SimpleGraph V) [DecidableRel G.Adj] :
    ‚àÉ s : Finset V, (s : Set V) ‚äÜ G.support ‚àß
      c*#s ‚â§ (G.induce s).minDegree ‚àß
      #(G.induce s).edgeFinset ‚â• #G.edgeFinset
        -c*(card G.support^2-#s^2)/2-c*(card G.support-#s)/2 := by
  by_cases hŒ¥ : c*#G.support.toFinset ‚â§ (G.induce G.support.toFinset).minDegree
  ¬∑ refine ‚ü®G.support.toFinset, by simp, hŒ¥, ?_‚ü©
    suffices h_card_edges : #(G.induce G.support).edgeFinset ‚â• #G.edgeFinset
        -c*(card G.support^2-#G.support.toFinset^2)/2-c*(card G.support-#G.support.toFinset)/2 by
      convert h_card_edges
      all_goals simp
    rw [card_edgeFinset_induce_support, ‚Üê Set.toFinset_card G.support]
    ring_nf; rfl
  ¬∑ replace hŒ¥ : (G.induce G.support).minDegree < c*(card G.support) := by
      push_neg at hŒ¥
      convert hŒ¥
      all_goals simp
    have h_card_support_pos : 0 < card G.support := by
      contrapose! hŒ¥
      rw [Nat.eq_zero_of_le_zero hŒ¥, Nat.cast_zero, mul_zero]
      exact Nat.cast_nonneg (G.induce G.support).minDegree
    haveI : Nonempty G.support := card_pos_iff.mp h_card_support_pos
    -- delete minimal degree vertex
    have ‚ü®x, hŒ¥_eq_degx‚ü© := exists_minimal_degree_vertex (G.induce G.support)
    let G' := G.deleteIncidenceSet ‚Üëx
    have ‚ü®s, hs', ihŒ¥', ih_card_edges'‚ü© :=
      exists_induced_subgraph_for_minDegree hc‚ÇÄ hc‚ÇÅ G'
    have ‚ü®hs, hx_not_mem‚ü© : (s : Set V) ‚äÜ G.support ‚àß ‚Üëx ‚àâ (s : Set V) := by
      rw [‚Üê Set.disjoint_singleton_right, ‚Üê Set.subset_diff]
      exact hs'.trans (G.deleteIncidenceSet_support_subset ‚Üëx)
    have ihŒ¥ : c*#s ‚â§ (G.induce s).minDegree := by
      simpa [‚Üê deleteIncidenceSet_induce_of_not_mem G hx_not_mem] using ihŒ¥'
    have ih_card_edges : #(G.induce s).edgeFinset ‚â• #G'.edgeFinset
        -c*((card G'.support)^2-#s^2)/2-c*((card G'.support)-#s)/2 := by
      simpa [sub_sub, Set.toFinset_card,
        ‚Üê G.deleteIncidenceSet_induce_of_not_mem hx_not_mem] using ih_card_edges'
    refine ‚ü®s, hs, ihŒ¥, ?_‚ü©
    calc (#(G.induce s).edgeFinset : ‚Ñù)
      _ ‚â• #G'.edgeFinset-(c*((card G'.support)^2-#s^2)/2+c*(card G'.support-#s)/2) := by
          rwa [sub_sub] at ih_card_edges
      _ ‚â• (#G.edgeFinset-c*(card G.support))
            -(c*((card G.support-1)^2-#s^2)/2+c*(card G.support-1-#s)/2) := by
          apply sub_le_sub
          -- exactly `G.minDegree` edges are deleted from the edge set
          ¬∑ rw [G.card_edgeFinset_deleteIncidenceSet ‚Üëx,
              Nat.cast_sub (G.degree_le_card_edgeFinset x),
              ‚Üê degree_induce_support, ‚Üê hŒ¥_eq_degx]
            exact sub_le_sub_left hŒ¥.le #G.edgeFinset
          -- at least 1 vertex is deleted from the support
          ¬∑ rw [‚Üê add_div, ‚Üê add_div, div_le_div_iff_of_pos_right zero_lt_two]
            apply add_le_add
            ¬∑ apply mul_le_mul_of_nonneg_left _ hc‚ÇÄ
              rw [sub_le_sub_iff_right]
              apply pow_le_pow_left‚ÇÄ (Nat.cast_nonneg (card G'.support))
              rw [‚Üê Nat.cast_pred card_pos]
              exact_mod_cast G.card_support_deleteIncidenceSet x.prop
            ¬∑ apply mul_le_mul_of_nonneg_left _ hc‚ÇÄ
              rw [sub_le_sub_iff_right, ‚Üê Nat.cast_pred card_pos]
              exact_mod_cast G.card_support_deleteIncidenceSet x.prop
      _ ‚â• #G.edgeFinset-c*((card G.support)^2-#s^2)/2-c*(card G.support-#s)/2 := by linarith
termination_by card G.support
decreasing_by
  exact (G.card_support_deleteIncidenceSet x.prop).trans_lt
    (Nat.pred_lt_of_lt h_card_support_pos)

/-- Repeatedly remove minimal degree vertices until `(G.induce s).minDegree` is
at least `c*#s` and `#s ‚âà ‚àöŒµ * (card V)` when `c ‚âà 0`.

This is an auxiliary definition for the **Erd≈ës-Stone theorem**. -/
private lemma exists_induced_subgraph_for_minDegree_for_card_sq
    {c Œµ : ‚Ñù} (hc‚ÇÄ : 0 ‚â§ c) (hŒµ : 0 ‚â§ Œµ) (h : #G.edgeFinset ‚â• (c+Œµ)*(card V)^2/2) :
    ‚àÉ s : Finset V, c*#s ‚â§ (G.induce s).minDegree ‚àß Œµ*(card V)^2-c*(card V) ‚â§ #s^2 := by
  rcases isEmpty_or_nonempty V
  ¬∑ use ‚àÖ
    simp
  ¬∑ have hc‚ÇÅ : c ‚â§ 1 := by
      by_contra! hc‚ÇÅ
      absurd calc ((card V)^2/2 : ‚Ñù)
        _ < (c+Œµ)*(card V)^2/2 := by
            apply div_lt_div_of_pos_right _ zero_lt_two
            apply lt_mul_of_one_lt_left (by positivity)
            exact hc‚ÇÅ.trans_le (le_add_of_nonneg_right hŒµ)
        _ ‚â§ #G.edgeFinset := h
      push_neg
      calc (#G.edgeFinset : ‚Ñù)
        _ ‚â§ (card V)*(card V-1)/2 := by
            rw [‚Üê Nat.cast_choose_two]
            exact_mod_cast card_edgeFinset_le_card_choose_two
        _ ‚â§ (card V)^2/2 := by
            rw [div_le_div_iff_of_pos_right zero_lt_two]
            nlinarith
    have ‚ü®s, _, hŒ¥, hs‚ü© := exists_induced_subgraph_for_minDegree hc‚ÇÄ hc‚ÇÅ G
    rw [ge_iff_le, sub_sub, sub_le_iff_le_add] at hs
    use s, hŒ¥
    rw [‚Üê div_le_div_iff_of_pos_right zero_lt_two, sub_div]
    calc Œµ*(card V)^2/2-c*(card V)/2
      _ = (c+Œµ)*(card V)^2/2-(c*(card V)^2/2+c*(card V)/2) := by ring_nf
      _ ‚â§ (#s*(#s-1)/2+(c*((card G.support)^2-#s^2)/2+c*(card G.support-#s)/2))
            -(c*(card V)^2/2+c*card V/2) := by
          apply sub_le_sub_right
          apply h.trans
          apply hs.trans
          apply add_le_add_right
          rw [‚Üê Nat.cast_choose_two, ‚Üê card_coe s]
          exact_mod_cast card_edgeFinset_le_card_choose_two
      _ = #s^2/2-(c*((card V)^2-(card G.support)^2)/2 + c*(card V-card G.support)/2
            +c*#s^2/2+c*#s/2+#s/2) := by ring_nf
      _ ‚â§ #s^2/2 := by
          apply sub_le_self
          repeat apply add_nonneg
          any_goals
            apply div_nonneg _ zero_le_two
            try apply mul_nonneg hc‚ÇÄ
            try apply sub_nonneg_of_le
            try apply pow_le_pow_left‚ÇÄ
            try positivity
            try exact_mod_cast set_fintype_card_le_univ G.support

/-- If `G` has at least `(1-1/r+o(1))*(card V)^2/2` many edges, then `G` contains a copy of
a `completeEquipartiteGraph` in `r+1` parts each of size `t`.

This is the **Erd≈ës-Stone theorem**. -/
theorem completeEquipartiteGraph_isContained_of_card_edgeFinset {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) (r t : ‚Ñï) :
    ‚àÉ n, ‚àÄ {V : Type*} [Fintype V] [DecidableEq V], n < card V ‚Üí
      ‚àÄ {G : SimpleGraph V} [DecidableRel G.Adj],
        #G.edgeFinset ‚â• (1-1/r+Œµ)*(card V)^2/2
        ‚Üí completeEquipartiteGraph (r+1) t ‚äë G := by
  -- choose `c + Œµ' = (1-1/r+Œµ/2) + Œµ/2 = 1-1/r+Œµ`
  let Œµ' := Œµ/2
  have hŒµ' : 0 < Œµ' := by positivity
  let c := 1-1/r+Œµ/2
  have hc : 0 < c := add_pos_of_nonneg_of_pos r.one_sub_one_div_cast_nonneg hŒµ'
  -- minimal-degree version of Erd≈ës-Stone theorem
  have ‚ü®n', ih‚ü© := completeEquipartiteGraph_isContained_of_minDegree hŒµ' r t
  use ‚åäc/Œµ'+n'/‚àöŒµ'‚åã‚Çä
  intro V _ _ h_cardV G _ h
  rw [Nat.floor_lt (by positivity)] at h_cardV
  -- prove `s` satisfies conditions for minimal-degree version
  rw [‚Üê add_halves Œµ, ‚Üê add_assoc] at h
  obtain ‚ü®s, hŒ¥, h_cards_sq‚ü© := exists_induced_subgraph_for_minDegree_for_card_sq hc.le hŒµ'.le h
  suffices h_cards : (n'^2 : ‚Ñù) < (#s^2 : ‚Ñù) by
    rw [‚Üê Nat.cast_pow, ‚Üê Nat.cast_pow, Nat.cast_lt,
      Nat.pow_lt_pow_iff_left two_ne_zero] at h_cards
    -- find `completeEquipartiteGraph` from minimal-degree version
    simp_rw [‚Üê card_coe, ‚Üê Finset.coe_sort_coe] at h_cards hŒ¥
    exact (ih h_cards hŒ¥).trans ‚ü®Copy.induce G s‚ü©
  -- `x ‚Ü¶ Œµ'*x^2-cx` is strictly monotonic on `[c/(2*Œµ'), ‚àû)`
  have h_strictMonoOn : StrictMonoOn (fun x ‚Ü¶ Œµ'*x^2-c*x) (Set.Ici (c/(2*Œµ'))) := by
    apply strictMonoOn_of_deriv_pos (convex_Ici _) (Continuous.continuousOn (by continuity))
    intro x hx
    rw [deriv_sub, deriv_const_mul, deriv_pow 2, Nat.cast_two, pow_one, ‚Üê mul_assoc Œµ' 2 x,
      mul_comm Œµ' 2, deriv_const_mul, deriv_id'', mul_one, sub_pos,
      ‚Üê div_lt_iff‚ÇÄ' (mul_pos two_pos hŒµ')]
    rwa [interior_Ici, Set.mem_Ioi] at hx
    all_goals
      try apply DifferentiableAt.const_mul
      try apply DifferentiableAt.pow
      exact differentiableAt_id'
  calc (#s^2 : ‚Ñù)
    _ ‚â• Œµ'*(card V)^2-c*(card V) := h_cards_sq
    _ > Œµ'*(c/Œµ'+n'/‚àöŒµ')^2-c*(c/Œµ'+n'/‚àöŒµ') := by
        have h_le : c/(2*Œµ') ‚â§ c/Œµ'+n'/‚àöŒµ' := by
          trans c/Œµ'
          ¬∑ rw [mul_comm, ‚Üê div_div, half_le_self_iff]
            exact div_nonneg hc.le hŒµ'.le
          ¬∑ rw [le_add_iff_nonneg_right]
            exact div_nonneg n'.cast_nonneg (sqrt_nonneg Œµ')
        exact h_strictMonoOn h_le (h_le.trans (mod_cast h_cardV.le)) h_cardV
    _ = n'^2+n'*c/sqrt Œµ' := by
        rw [add_pow_two, mul_add, div_pow (n' : ‚Ñù) ‚àöŒµ', sq_sqrt hŒµ'.le,
          mul_div_cancel‚ÇÄ _ hŒµ'.ne', mul_add, pow_two (c/Œµ'), ‚Üê mul_assoc,
          mul_comm 2 (c/Œµ'), mul_assoc (c/Œµ') 2 _, ‚Üê mul_assoc, mul_div_cancel‚ÇÄ _ hŒµ'.ne']
        ring_nf
    _ ‚â• n'^2 := le_add_of_nonneg_right (by positivity)

end ErdosStone

section ErdosStoneSimonovits

/-- If `Œµ > 2*r/n` then a `completeEquipartiteGraph` in `r` parts each of size `‚åän/r‚åã` has
more than `(1-1/r-Œµ)*n^2/2` edges.

This is an auxiliary definition for the **Erd≈ës-Stone-Simonovits theorem**. -/
lemma card_edgeFinset_completeEquipartiteGraph_gt {r n : ‚Ñï} (hr : 0 < r) (hn : 0 < n) :
    ‚àÄ Œµ > (2*r/n : ‚Ñù),
      (1-1/r-Œµ)*n^2/2 < #(completeEquipartiteGraph r (n/r)).edgeFinset := by
  let t := n/r
  intro Œµ hŒµ
  rw [gt_iff_lt, div_lt_iff‚ÇÄ (by positivity)] at hŒµ
  calc (1-1/r-Œµ)*n^2/2
    _ < (1-1/r)*n^2/2-r*n := by
        rw [sub_mul, sub_div, sub_lt_sub_iff_left, lt_div_iff‚ÇÄ zero_lt_two,
          mul_comm, ‚Üê mul_assoc, pow_two, ‚Üê mul_assoc]
        exact mul_lt_mul_of_pos_right hŒµ (mod_cast hn)
    _ = (1-1/r)*r^2*t^2/2-(r*n-(1-1/r)*(n*‚Üë(n%r))+(1-1/r)*‚Üë(n % r)^2/2) := by
        conv_lhs =>
          lhs; rw [‚Üê n.div_add_mod r, Nat.cast_add, add_sq, add_assoc, mul_add, add_div]
          lhs; rw [Nat.cast_mul, mul_pow, ‚Üê mul_assoc]
        rw [‚Üê Nat.sub_mod_eq_mul_div, Nat.cast_sub (n.mod_le r)]
        ring_nf
    _ ‚â§ (1-1/r)*r^2*t^2/2 := by
        apply sub_le_self
        apply add_nonneg
        ¬∑ rw [sub_nonneg, ‚Üê mul_assoc, mul_comm (r : ‚Ñù) (n : ‚Ñù)]
          apply mul_le_mul _ (mod_cast (n.mod_lt hr).le) (n%r).cast_nonneg (mod_cast hn.le)
          rw [mul_le_iff_le_one_left (mod_cast hn)]
          exact r.one_sub_one_div_cast_le_one
        ¬∑ apply div_nonneg _ zero_le_two
          exact mul_nonneg (r.one_sub_one_div_cast_nonneg) (by positivity)
    _ = #(completeEquipartiteGraph r t).edgeFinset := by
        simp_rw [card_edgeFinset_completeEquipartiteGraph,
          Nat.cast_mul, Nat.cast_pow, Nat.cast_choose_two]
        field_simp
        ring_nf

variable {W : Type*} [Fintype W] {H : SimpleGraph W}

omit [Fintype W] in
lemma lt_extremalNumber_of_not_colorable {Œµ : ‚Ñù} (hŒµ : 0 < Œµ)
    {r : ‚Ñï} (hr : 0 < r) (nhc : ¬¨H.Colorable r) :
    ‚àÉ n, ‚àÄ {V : Type*} [Fintype V] [DecidableEq V], n < card V ‚Üí
      (1-1/r-Œµ)*(card V)^2/2 < extremalNumber (card V) H := by
  use ‚åà2*r/Œµ‚åâ‚Çä
  intro V _ _ h_cardV
  haveI : Nonempty V := by
    rw [‚Üê card_pos_iff]
    exact Nat.zero_lt_of_lt h_cardV
  have hŒµ' : 2*r/card V < Œµ := by
    rw [div_lt_iff‚ÇÄ (by positivity), ‚Üêdiv_lt_iff‚ÇÄ' hŒµ]
    exact (Nat.le_ceil _).trans_lt (mod_cast h_cardV)
  let t := card V/r
  let G : SimpleGraph (Fin r √ó Fin t) := completeEquipartiteGraph r t
  -- `completeEquipartiteGraph` is `r`-colorable
  haveI : Nonempty (Fin r √ó Fin t ‚Ü™ V) := by
    apply Function.Embedding.nonempty_of_card_le
    simpa [card_prod, Fintype.card_fin] using (card V).mul_div_le r
  let f := Classical.arbitrary (Fin r √ó Fin t ‚Ü™ V)
  -- `completeEquipartiteGraph` has the right amount of edges
  have h_card_edges : #G.edgeFinset > (1-1/r-Œµ)*(card V)^2/2 :=
    card_edgeFinset_completeEquipartiteGraph_gt hr card_pos Œµ hŒµ'
  rw [‚Üê G.card_edgeFinset_map f] at h_card_edges
  apply lt_of_lt_of_le h_card_edges
  rw [Nat.cast_le]
  -- `completeEquipartiteGraph` does not contain `H`
  apply le_extremalNumber
  haveI : NeZero r := ‚ü®hr.ne'‚ü©
  exact free_of_colorable nhc (completeEquipartiteGraph_colorable.map f)

lemma extremalNumber_le_of_colorable {Œµ : ‚Ñù} (hŒµ : 0 < Œµ)
    {r : ‚Ñï} (hc : H.Colorable (r+1)) :
    ‚àÉ n, ‚àÄ {V : Type*} [Fintype V] [DecidableEq V], n < card V ‚Üí
      extremalNumber (card V) H ‚â§ (1-1/r+Œµ)*(card V)^2/2 := by
  have ‚ü®t, h_isContained_lhs‚ü© := isContained_completeEquipartiteGraph_of_colorable hc
  have ‚ü®n, h_isContained_rhs‚ü© := completeEquipartiteGraph_isContained_of_card_edgeFinset hŒµ r t
  use n; intro V _ _ h_cardV
  trans (extremalNumber (card V) (completeEquipartiteGraph (r+1) t) : ‚Ñù)
  -- `completeEquipartiteGraph` contains `H`
  ¬∑ exact_mod_cast extremalNumber_of_isContained h_isContained_lhs
  -- `G` contains `completeEquipartiteGraph`
  ¬∑ have h : 0 ‚â§ (1-1/r+Œµ)*(card V)^2/2 := by
      apply div_nonneg _ zero_le_two
      apply mul_nonneg _ (by positivity)
      exact add_nonneg (Nat.one_sub_one_div_cast_nonneg r) hŒµ.le
    rw [extremalNumber_le_iff_of_nonneg _ h]
    intro _ _ h
    contrapose! h
    rw [not_free]
    exact h_isContained_rhs h_cardV h.le

/-- If the chromatic number of `H` equals `r+1 > 0`, then `extremalNumber` of `H` is greater
than `(1-1/r-o(1))*(card V)^2/2` and at most `(1-1/r+o(1))*(card V)^2/2`.

This is the **Erd≈ës-Stone-Simonovits theorem**. -/
theorem lt_extremalNumber_le_of_chromaticNumber {Œµ : ‚Ñù} (hŒµ : 0 < Œµ)
    {r : ‚Ñï} (hr : 0 < r) (hœá : H.chromaticNumber = r+1) :
    ‚àÉ n, ‚àÄ {V : Type*} [Fintype V] [DecidableEq V], n < card V ‚Üí
      (1-1/r-Œµ)*(card V)^2/2 < extremalNumber (card V) H ‚àß
      extremalNumber (card V) H ‚â§ (1-1/r+Œµ)*(card V)^2/2 := by
  have ‚ü®hc, nhc‚ü© := chromaticNumber_eq_iff_colorable_not_colorable.mp hœá
  have ‚ü®n‚ÇÅ, h‚ÇÅ‚ü© := lt_extremalNumber_of_not_colorable hŒµ hr nhc
  have ‚ü®n‚ÇÇ, h‚ÇÇ‚ü© := extremalNumber_le_of_colorable hŒµ hc
  use max n‚ÇÅ n‚ÇÇ; intro V _ _ h_cardV
  have h_cardV‚ÇÅ := h_cardV.trans_le' (Nat.le_max_left n‚ÇÅ n‚ÇÇ)
  have h_cardV‚ÇÇ := h_cardV.trans_le' (Nat.le_max_right n‚ÇÅ n‚ÇÇ)
  exact ‚ü®h‚ÇÅ h_cardV‚ÇÅ, h‚ÇÇ h_cardV‚ÇÇ‚ü©

/-- If the chromatic number of `H` equals `r+1 > 0`, then the `extremalNumber` of `H` is equal
to `(1-1/r+o(1))*n^2/2`.

This is a corollary of the **Erd≈ës-Stone-Simonovits theorem**. -/
theorem isLittleO_extremalNumber_of_chromaticNumber
    {r : ‚Ñï} (hr : 0 < r) (hœá : H.chromaticNumber = r+1) :
    (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber n H-(1-1/r)*n^2/2 : ‚Ñù))
      =o[atTop] (fun (n : ‚Ñï) ‚Ü¶ (n^2 : ‚Ñù)) := by
  rw [isLittleO_iff]
  intro Œµ hŒµ
  rw [eventually_atTop]
  have ‚ü®n‚ÇÄ, h‚ü© := lt_extremalNumber_le_of_chromaticNumber hŒµ hr hœá
  use n‚ÇÄ + 1; intro n (hn : n‚ÇÄ < n)
  rw [‚Üê Fintype.card_fin n] at hn
  specialize h hn
  rw [Fintype.card_fin] at h
  rw [norm_eq_abs, ‚Üê abs_of_pos hŒµ, norm_eq_abs, ‚Üê abs_mul, ‚Üê sq_le_sq]
  apply sq_le_sq'
  all_goals linarith

/-- If the chromatic number of `H` equals `r+1 > 0`, then the limit
`extremalNumber n H / n.choose 2` approaches `1-1/r` as `n ‚Üí ‚àû`.

This is a corollary of the **Erd≈ës-Stone-Simonovits theorem**. -/
theorem tendsto_extremalNumber_div_choose_two_of_chromaticNumber
    {r : ‚Ñï} (hr : 0 < r) (hœá : H.chromaticNumber = r+1) :
    Tendsto (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber n H / n.choose 2 : ‚Ñù)) atTop (ùìù (1-1/r)) := by
  have hz : ‚àÄ·∂† (n : ‚Ñï) in atTop, (n.choose 2 : ‚Ñù) ‚â† 0 := by
    rw [eventually_atTop]
    use 2; intro n hn
    exact_mod_cast (Nat.choose_pos hn).ne'
  have h_tendsto : Tendsto (fun (n : ‚Ñï) ‚Ü¶ ((n^2/2)/(n.choose 2) : ‚Ñù)) atTop (ùìù 1) := by
    simpa [isEquivalent_iff_tendsto_one hz] using (isEquivalent_choose 2).symm
  have h_littleo := IsLittleO.trans_isTheta
    (isLittleO_extremalNumber_of_chromaticNumber hr hœá) (isTheta_choose 2).symm
  simpa [sub_div, ‚Üê mul_div]
    using h_littleo.tendsto_div_nhds_zero.add (h_tendsto.const_mul (1-1/r : ‚Ñù))

/-- If the chromatic number of `H` equals `r+1 > 0`, then the Tur√°n density of `H`
equals `1-1/r`.

This is a corollary of the **Erd≈ës-Stone-Simonovits theorem**. -/
theorem turanDensity_eq_of_chromaticNumber
    {r : ‚Ñï} (hr : 0 < r) (hœá : H.chromaticNumber = r+1) : turanDensity H = 1-1/r :=
  Tendsto.limUnder_eq (tendsto_extremalNumber_div_choose_two_of_chromaticNumber hr hœá)

/-- If the chromatic number of `H` equals `r+1 > 1`, then `extremalNumber n H` is
asymptotically equivalent to `(1-1/r)*(n.choose 2)` as `n ‚Üí ‚àû`

This is a corollary of the **Erd≈ës-Stone-Simonovits theorem**. -/
theorem isEquivalent_extremalNumber_of_chromaticNumber
    {r : ‚Ñï} (hr : 1 < r) (hœá : H.chromaticNumber = r+1) :
    (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber n H : ‚Ñù))
      ~[atTop] (fun (n : ‚Ñï) ‚Ü¶ ((1-1/r)*(n.choose 2) : ‚Ñù)) := by
  have hœÄ_eq : turanDensity H = 1-1/r := turanDensity_eq_of_chromaticNumber (by positivity) hœá
  have hœÄ_pos : 0 < turanDensity H := by
    rw [hœÄ_eq, sub_pos, one_div]
    exact inv_lt_one_of_one_lt‚ÇÄ (mod_cast hr)
  simpa [hœÄ_eq] using isEquivalent_extremalNumber hœÄ_pos.ne'

end ErdosStoneSimonovits

end SimpleGraph
