import Mathlib

import ErdosStoneSimonovits.Nat
import ErdosStoneSimonovits.QuadraticFunctions

import ErdosStoneSimonovits.Degree
import ErdosStoneSimonovits.Bipartite
import ErdosStoneSimonovits.SpecialGraphs

import ErdosStoneSimonovits.TuranDensity

open Topology Asymptotics Real

namespace SimpleGraph

/-- Suppose `r`, `t` are natural numbers and `Œµ > 0` is a real number. A simple
graph of sufficently many vertices `n` having a minimal degree of at least
`(1-1/r+Œµ)*n` contains a copy of a complete equipartite graph in `r+1`
partitions each of size `t` as an isomorphic subgraph.

This is the minimal-degree version of the *Erd≈ës-Stone theorem*. -/
theorem isIsoSubgraph_completeEquipartiteGraph_of_minDegree
    {Œµ : ‚Ñù} (hŒµ_pos : 0 < Œµ) (r t : ‚Ñï) :
    ‚àÉ N, ‚àÄ {V : Type u} [Fintype V] [DecidableEq V], N < Fintype.card V ‚Üí
      ‚àÄ {G : SimpleGraph V} [DecidableRel G.Adj],
        G.minDegree ‚â• (1-1/r+Œµ)*(Fintype.card V)
        ‚Üí (completeEquipartiteGraph (Fin (r+1)) (Fin t)).IsIsoSubgraph G := by
  by_cases hr_or_t : r = 0 ‚à® t = 0
  -- base case
  . cases hr_or_t with
    | inl hr =>
      use t
      intro V _ _ h_cardV G _ _
      rw [hr, zero_add]
      apply isIsoSubgraph_of_isEmpty_edgeSet
      simp_rw [Fintype.card_prod, Fintype.card_fin, one_mul]
      exact le_of_lt h_cardV
    | inr ht =>
      use 0
      intros
      rw [ht]
      exact isIsoSubgraph_of_isEmpty
  -- inductive step
  . have ‚ü®hr_pos, ht_pos‚ü© : 0 < r ‚àß 0 < t := by
      push_neg at hr_or_t
      simpa [‚ÜêNat.pos_iff_ne_zero] using hr_or_t
    haveI : Nonempty (Fin t) := by
      rw [‚ÜêFintype.card_pos_iff, Fintype.card_fin]
      exact ht_pos
    -- choose Œµ' > 0
    let Œµ' := 1/((r-1)*r) + Œµ
    have hŒµ'_pos : 0 < Œµ' := by
      apply add_pos_of_nonneg_of_pos _ hŒµ_pos
      rw [one_div_nonneg]
      field_simp
      exact hr_pos
    -- choose `t' > 0`
    let t' := ‚åàt/(r*Œµ)‚åâ‚Çä + 1
    have ht'_pos : 0 < t' :=
      lt_of_lt_of_le zero_lt_one (Nat.le_add_left _ _)
    -- inductive hypothesis
    have ‚ü®N, ih‚ü© :=
      isIsoSubgraph_completeEquipartiteGraph_of_minDegree hŒµ'_pos (r-1) t'
    let N' := max N ‚åà((t'.choose t)^r*t+r*t')*(t'-t)/(t'*r*Œµ-t)‚åâ‚Çä
    use N'
    intro V _ _ hN'_lt_cardV G _ hŒ¥_ge
    have hN_lt_cardV : N < Fintype.card V :=
      lt_of_le_of_lt (le_max_left _ _) hN'_lt_cardV
    -- inequalities involving `r`, `t`, `t'`, `Œµ`, `Œµ'`
    have hrŒµ_pos : 0 < r*Œµ := by
      apply mul_pos _ hŒµ_pos
      rw [Nat.cast_pos]
      exact hr_pos
    have hrŒµ_lt_one : r*Œµ < 1 := by
      by_contra! nh
      rw [‚Üêdiv_le_iff‚ÇÄ'] at nh
      absurd calc (G.minDegree : ‚Ñù)
        _ ‚â• (1-1/r+Œµ)*(Fintype.card V) := hŒ¥_ge
        _ ‚â• (Fintype.card V) := by
            apply le_mul_of_one_le_left (Nat.cast_nonneg _)
            rw [sub_add, le_sub_self_iff, sub_nonpos]
            exact nh
      push_neg
      rw [Nat.cast_lt]
      haveI : Nonempty V := by
        rw [‚ÜêFintype.card_pos_iff]
        exact lt_of_le_of_lt (Nat.zero_le _) hN'_lt_cardV
      exact minDegree_lt_card G
      rw [Nat.cast_pos]
      exact hr_pos
    have ht_lt_t'rŒµ : t < t'*r*Œµ := by
      rw [mul_assoc, ‚Üêdiv_mul_cancel‚ÇÄ (t : ‚Ñù) hrŒµ_pos.ne',
        mul_lt_mul_right hrŒµ_pos, Nat.cast_add_one]
      exact lt_of_lt_of_le (lt_add_of_pos_right _ zero_lt_one)
        (add_le_add_right (Nat.le_ceil _) _)
    have ht_lt_t' : (t : ‚Ñù) < (t' : ‚Ñù) := by
      replace ht'_pos : 0 < (t' : ‚Ñù) := by
        rw [Nat.cast_pos]
        exact ht'_pos
      rw [‚Üêmul_lt_iff_lt_one_right ht'_pos, ‚Üêmul_assoc] at hrŒµ_lt_one
      exact ht_lt_t'rŒµ.trans hrŒµ_lt_one
    replace ht_lt_t' : t < t' := by
      rwa [Nat.cast_lt] at ht_lt_t'
    have ht'_le_N' : (t' : ‚Ñù) ‚â§ (N' : ‚Ñù) :=
      calc (t' : ‚Ñù)
        _ ‚â§ r*t' := by
            rw [le_mul_iff_one_le_left, Nat.one_le_cast]
            exact hr_pos
            rw [Nat.cast_pos]
            exact ht'_pos
        _ ‚â§ (t'.choose t)^r*t + r*t' := by field_simp
        _ ‚â§ ((t'.choose t)^r*t + r*t')*(t'-t)/(t'*r*Œµ-t) := by
            rw [mul_div_assoc, le_mul_iff_one_le_right, one_le_div]
            apply sub_le_sub_right
            rw [mul_assoc, mul_le_iff_le_one_right]
            exact le_of_lt hrŒµ_lt_one
            rw [Nat.cast_pos]
            exact ht'_pos
            rw [sub_pos]
            exact ht_lt_t'rŒµ
            apply add_pos_of_nonneg_of_pos
            all_goals field_simp
        _ ‚â§ N' := by
            apply (Nat.le_ceil _).trans
            rw [Nat.cast_le]
            exact le_max_right N _
    replace ht'_le_N' : t' ‚â§ N' := by rwa [Nat.cast_le] at ht'_le_N'
    -- identify `completeEquipartiteGraph (Fin r) (Fin t')`
    have h_iso_sub :
        (completeEquipartiteGraph (Fin r) (Fin t')).IsIsoSubgraph G := by
      by_cases hr_one : r = 1
      . rw [hr_one]
        apply isIsoSubgraph_of_isEmpty_edgeSet
        simp_rw [Fintype.card_prod, Fintype.card_fin, one_mul]
        exact le_of_lt (lt_of_le_of_lt ht'_le_N' hN'_lt_cardV)
      . have hr_ne : (r : ‚Ñù) ‚â† 0 := by
          rw [Nat.cast_ne_zero]
          exact hr_pos.ne'
        have hrpred_ne : (r-1 : ‚Ñù) ‚â† 0 := by
          rw [sub_ne_zero, Nat.cast_ne_one]
          exact hr_one
        have hŒ¥_ge' : G.minDegree ‚â• (1-1/‚Üë(r-1)+Œµ')*(Fintype.card V) :=
          calc (G.minDegree : ‚Ñù)
            _ ‚â• (1-1/(r-1)+(1/(r-1)-1/r)+Œµ)*(Fintype.card V) := by
                rw [sub_add_sub_cancel]
                exact hŒ¥_ge
            _ = (1-1/(r-1)+1/((r-1)*r)+Œµ)*(Fintype.card V) := by
                rw [‚Üêone_div_mul_sub_mul_one_div_eq_one_div_add_one_div
                  hrpred_ne hr_ne]
                field_simp
            _ = (1-1/‚Üë(r-1)+Œµ')*(Fintype.card V) := by
                rw [Nat.cast_pred hr_pos]
                ring_nf
        rw [‚ÜêNat.succ_pred_eq_of_pos hr_pos]
        exact ih hN_lt_cardV hŒ¥_ge'
    rw [isIsoSubgraph_completeEquipartiteGraph_iff] at h_iso_sub
    -- `A‚ÇÅ, ‚Ä¶, A·µ£` partitions of `completeEquipartiteGraph (Fin r) (Fin t')`
    have ‚ü®A, hA‚ü© := h_iso_sub
    have h_cardA (i : Fin r) : (A i).val.card = t' := by
      have h_mem := (A i).prop
      simp_rw [Finset.mem_powersetCard, Fintype.card_fin t'] at h_mem
      exact h_mem.2
    have h_pairwise_disjointA :
        Set.univ.PairwiseDisjoint (Subtype.val ‚àò (A ¬∑)) := by
      intro i‚ÇÅ _ i‚ÇÇ _ hi
      rw [Function.onFun_apply, Finset.disjoint_left]
      intro v hi‚ÇÅ
      by_contra! hi‚ÇÇ
      absurd G.loopless v
      apply hA i‚ÇÅ i‚ÇÇ hi v hi‚ÇÅ v hi‚ÇÇ
    rw [‚ÜêFinset.coe_univ] at h_pairwise_disjointA
    -- `U` vertices in `A‚ÇÅ ‚à™ ‚Ä¶ ‚à™ A·µ£`
    let U : Finset V :=
      (Finset.univ.disjiUnion (Subtype.val ‚àò (A ¬∑)) h_pairwise_disjointA)·∂ú
    have h_cardUcompl : U·∂ú.card = r*t' := by
      simp_rw [compl_compl _, Finset.card_disjiUnion, Function.comp_apply,
        h_cardA, Finset.sum_const, Finset.card_univ, Fintype.card_fin,
        smul_eq_mul]
    -- `W ‚äÜ U` vertices adjacent to at least `t` vertices in each `A‚ÇÅ, ‚Ä¶, A·µ£`
    let W := U.filter fun v ‚Ü¶
      ‚àÄ i : Fin r, ‚àÉ s : Finset.powersetCard t (A i).val,
        ‚àÄ w ‚àà s.val, G.Adj v w
    -- `W.card` is arbitrarily large
    have h_cardW : ((t'.choose t)^r*t : ‚Ñù) < (W.card : ‚Ñù) := by
      -- double-counting edges between `U` and `U·∂ú`
      have h_between_isBipartite :
          (G.between U (‚ÜëU)·∂ú).IsBipartite U (U·∂ú : Finset V) := by
        rw [Finset.coe_compl U]
        exact isBipartite_between_of_disjoint (disjoint_compl_right)
      -- counting over `U·∂ú`
      have h_le_between :
          U·∂ú.card*((1-1/r+Œµ)*(Fintype.card V)-U·∂ú.card)
            ‚â§ ((G.between U (‚ÜëU)·∂ú).edgeFinset.card : ‚Ñù) :=
        calc U·∂ú.card*((1-1/r+Œµ)*(Fintype.card V)-U·∂ú.card)
          _ ‚â§ U·∂ú.card*(G.minDegree-U·∂ú.card) := by
              apply mul_le_mul_of_nonneg_left
              apply sub_le_sub_right hŒ¥_ge
              exact Nat.cast_nonneg _
          _ = ‚àë _ ‚àà U·∂ú, (G.minDegree-U·∂ú.card : ‚Ñù) := by
              rw [Finset.sum_const]
              ring_nf
          _ ‚â§ ‚àë v ‚àà U·∂ú, (G.between U (‚ÜëU)·∂ú).degree v := by
              rw [Nat.cast_sum]
              apply Finset.sum_le_sum
              intro v hv
              rw [sub_le_iff_le_add, ‚ÜêNat.cast_add, Nat.cast_le]
              exact (minDegree_le_degree G v).trans
                (degree_le_between_degree_plus_card' U hv)
          _ = (G.between U (‚ÜëU)·∂ú).edgeFinset.card := by
              rw [Nat.cast_inj, eq_comm]
              convert isBipartite_sum_degrees_eq_card_edges'
                h_between_isBipartite
      -- if `v ‚àà U\W`, then `G.degree v < (r-1)*t'+t`, else `v ‚àà W`
      have h_degree_lt {v : V} (hv : v ‚àà U\W) :
          (G.between U (‚ÜëU)·∂ú).degree v < U·∂ú.card-t'+t := by
        rw [Finset.mem_sdiff, Finset.mem_filter] at hv
        push_neg at hv
        have ‚ü®i, hs‚ü© := hv.2 hv.1
        rw [h_cardUcompl, ‚ÜêNat.sub_one_mul, ‚Üêcard_neighborFinset_eq_degree,
          isBipartite_neighborFinset h_between_isBipartite hv.1, compl_compl,
          Finset.disjiUnion_eq_biUnion, Finset.filter_biUnion,
          Finset.card_biUnion, ‚ÜêFinset.union_compl {i}, Finset.union_comm,
          Finset.sum_union disjoint_compl_left, Finset.sum_singleton]
        apply add_lt_add_of_le_of_lt
        . calc _
            _ ‚â§ ‚àë i' ‚àà {i}·∂ú, (A i').val.card := by
                apply Finset.sum_le_sum
                intro _ _
                exact Finset.card_filter_le _ _
            _ = (r-1)*t' := by
                simp_rw [h_cardA, Finset.sum_const, Finset.card_compl,
                  Fintype.card_fin, Finset.card_singleton, smul_eq_mul]
        . contrapose! hs
          rw [‚ÜêFinset.powersetCard_nonempty] at hs
          replace ‚ü®s, hs‚ü© := hs
          use ‚ü®s, Finset.powersetCard_mono (Finset.filter_subset _ _) hs‚ü©
          rw [Finset.mem_powersetCard] at hs
          intro w hw
          replace hw := hs.1 hw
          rw [Finset.mem_filter,
            between_adj_of_disjoint (disjoint_compl_right)] at hw
          exact hw.2.1
        intro i‚ÇÅ hi‚ÇÅ i‚ÇÇ hi‚ÇÇ h
        apply Finset.disjoint_filter_filter
        apply h_pairwise_disjointA hi‚ÇÅ hi‚ÇÇ h
      -- counting over `U`
      have h_between_le :
          ((G.between U (‚ÜëU)·∂ú).edgeFinset.card : ‚Ñù)
            ‚â§ ((Fintype.card V)-U·∂ú.card)*(U·∂ú.card-(t'-t))+W.card*(t'-t) :=
        calc ((G.between U (‚ÜëU)·∂ú).edgeFinset.card : ‚Ñù)
          _ = ‚àë v ‚àà U, ((G.between U (‚ÜëU)·∂ú).degree v : ‚Ñù) := by
              rw [‚ÜêNat.cast_sum, Nat.cast_inj]
              convert isBipartite_sum_degrees_eq_card_edges
                h_between_isBipartite
          _ = ‚àë v ‚àà U\W, ((G.between U (‚ÜëU)·∂ú).degree v : ‚Ñù)
                + ‚àë v ‚àà W, ((G.between U (‚ÜëU)·∂ú).degree v : ‚Ñù) := by
              rw [eq_comm, Finset.sum_sdiff]
              exact Finset.filter_subset _ _
          _ ‚â§ ‚àë _ ‚àà U\W, (U·∂ú.card-t'+t : ‚Ñù)
                + ‚àë _ ‚àà W, (U·∂ú.card : ‚Ñù) := by
              apply add_le_add
              all_goals apply Finset.sum_le_sum
              . intro v hv
                rw [‚ÜêNat.cast_sub, ‚ÜêNat.cast_add, Nat.cast_le]
                exact le_of_lt (h_degree_lt hv)
                rw [h_cardUcompl]
                apply Nat.le_mul_of_pos_left
                exact hr_pos
              . intro v hv
                rw [Nat.cast_le]
                exact isBipartite_degree_le h_between_isBipartite
                  (Finset.filter_subset _ _ hv)
          _ = ((Fintype.card V)-U·∂ú.card)*(U·∂ú.card-(t'-t))+W.card*(t'-t) := by
              have hW_sub : W ‚äÜ U := Finset.filter_subset _ _
              have h_cardW_le : W.card ‚â§ U.card :=
                Finset.card_le_card (Finset.filter_subset _ _)
              simp_rw [Finset.sum_const, Finset.card_sdiff hW_sub,
                nsmul_eq_mul, Nat.cast_sub h_cardW_le]
              conv_lhs =>
                lhs; lhs;
                rw [‚Üêcompl_compl U]
              have h_cardUcompl_le : U·∂ú.card ‚â§ Fintype.card V := by
                exact Finset.card_le_card (Finset.subset_univ _)
              simp_rw [Finset.card_compl U·∂ú, Nat.cast_sub h_cardUcompl_le]
              ring_nf
      have h_cardW_mul_right :
          ((t'.choose t)^r*t*(t'-t) : ‚Ñù) < (W.card*(t'-t) : ‚Ñù) :=
        calc ((t'.choose t)^r*t*(t'-t) : ‚Ñù)
          _ < (Fintype.card V)*(t'*r*Œµ-t)-r*t'*(t'-t) := by
              rw [‚Üêadd_sub_cancel_right (_^r*t : ‚Ñù) (r*t' : ‚Ñù), sub_mul,
                ‚Üêdiv_mul_cancel‚ÇÄ ((_^r*t+r*t')*(t'-t) : ‚Ñù)]
              apply sub_lt_sub_right
              . apply mul_lt_mul_of_pos_right
                . apply lt_of_le_of_lt (b := (N' : ‚Ñù))
                  . apply (Nat.le_ceil _).trans
                    rw [Nat.cast_le]
                    exact le_max_right _ _
                  . rw [Nat.cast_lt]
                    exact hN'_lt_cardV
                . rw [sub_pos]
                  exact ht_lt_t'rŒµ
              . rw [sub_ne_zero]
                exact ht_lt_t'rŒµ.ne'
          _ ‚â§ W.card*(t'-t) := by
              convert sub_left_le_of_le_add
                (h_le_between.trans h_between_le) using 1
              field_simp [h_cardUcompl]
              ring_nf
      rwa [mul_lt_mul_right] at h_cardW_mul_right
      rw [sub_pos, Nat.cast_lt]
      exact ht_lt_t'
    have h_cardW : (t'.choose t)^r*t < W.card := by
      rwa [‚ÜêNat.cast_pow, ‚ÜêNat.cast_mul, Nat.cast_lt] at h_cardW
    -- strong pigeonhole principle
    let F : W ‚Üí Œ† i : Fin r, Finset.powersetCard t (A i).val := by
      intro ‚ü®w, hw‚ü© i
      rw [Finset.mem_filter] at hw
      exact (hw.2 i).choose
    have hF (w : W) (i : Fin r) : ‚àÄ v ‚àà (F w i).val, G.Adj w v := by
      have hw := w.prop
      rw [Finset.mem_filter] at hw
      exact (hw.2 i).choose_spec
    haveI : Nonempty (Œ† i : Fin r, Finset.powersetCard t (A i).val) := by
      rw [Classical.nonempty_pi]; intro i
      rw [Finset.nonempty_coe_sort, Finset.powersetCard_nonempty, h_cardA i]
      exact le_of_lt ht_lt_t'
    have ‚ü®y, hy‚ü© : ‚àÉ y : Œ† i : Fin r, Finset.powersetCard t (A i).val,
        t ‚â§ (Finset.univ.filter (F ¬∑ = y)).card := by
      apply Fintype.exists_le_card_fiber_of_mul_le_card
      simp_rw [Fintype.card_pi, Fintype.card_coe, Finset.card_powersetCard,
        h_cardA, Finset.prod_const, Finset.card_univ, Fintype.card_fin]
      exact le_of_lt h_cardW
    rw [isIsoSubgraph_completeEquipartiteGraph_succ_iff, Fintype.card_fin t]
    -- identify `completeEquipartiteGraph (Fin r) (Fin t)`
    let A' (i : Fin r) : (Finset.univ : Finset V).powersetCard t := by
      use (y i).val
      have hyi := (y i).prop
      rw [Finset.mem_powersetCard] at hyi ‚ä¢
      exact ‚ü®Finset.subset_univ _, hyi.2‚ü©
    use A'
    constructor
    . intro i‚ÇÅ i‚ÇÇ hi v‚ÇÅ hv‚ÇÅ v‚ÇÇ hv‚ÇÇ
      apply hA i‚ÇÅ i‚ÇÇ hi
      . have hyi‚ÇÅ := (y i‚ÇÅ).prop
        rw [Finset.mem_powersetCard] at hyi‚ÇÅ
        exact hyi‚ÇÅ.1 hv‚ÇÅ
      . have hyi‚ÇÇ := (y i‚ÇÇ).prop
        rw [Finset.mem_powersetCard] at hyi‚ÇÇ
        exact hyi‚ÇÇ.1 hv‚ÇÇ
    -- identify `completeEquipartiteGraph (Fin (r+1)) (Fin t')`
    . have ‚ü®s', hs'‚ü© := Finset.exists_subset_card_eq hy
      let s : Finset.univ.powersetCard t := by
        use s'.map (Function.Embedding.subtype (¬∑ ‚àà W))
        rw [Finset.mem_powersetCard, Finset.card_map]
        exact ‚ü®Finset.subset_univ _, hs'.2‚ü©
      use s
      intro v‚ÇÅ hv‚ÇÅ i v‚ÇÇ hv‚ÇÇ
      have hv‚ÇÅ' := Finset.property_of_mem_map_subtype s' hv‚ÇÅ
      let v‚ÇÅ' : W := ‚ü®v‚ÇÅ, hv‚ÇÅ'‚ü©
      apply hF v‚ÇÅ' i v‚ÇÇ
      replace hv‚ÇÅ' : v‚ÇÅ' ‚àà s' := by
        rw [Finset.mem_map] at hv‚ÇÅ
        convert hv‚ÇÅ.choose_spec.1
        rw [Subtype.ext_iff]
        exact hv‚ÇÅ.choose_spec.2.symm
      replace hv‚ÇÅ' := hs'.1 hv‚ÇÅ'
      rw [Finset.mem_filter] at hv‚ÇÅ'
      rw [hv‚ÇÅ'.2]
      exact hv‚ÇÇ

/-- This auxiliary lemma encapsulates recursively removing minimal degree
vertices until `c*s.card ‚â§ (G.induce s).minDegree`. -/
private lemma exists_induced_subgraph_for_minDegree
    {c : ‚Ñù} (hc_nonneg : 0 ‚â§ c) (hc_le_one : c ‚â§ 1)
    {V : Type*} [Fintype V] [DecidableEq V]
    (G : SimpleGraph V) [DecidableRel G.Adj] :
    ‚àÉ s : Finset V,
      (s : Set V) ‚äÜ G.support ‚àß
      c*s.card ‚â§ (G.induce s).minDegree ‚àß
      (G.induce s).edgeFinset.card ‚â•
        G.edgeFinset.card
        -c*((Fintype.card G.support)^2-s.card^2)/2
        -(Fintype.card G.support-s.card)/2 := by
  by_cases hŒ¥ :
      c*G.support.toFinset.card ‚â§ (G.induce G.support.toFinset).minDegree
  -- base case
  . refine ‚ü®G.support.toFinset, by simp, hŒ¥, ?_‚ü©
    suffices h_coe_toFinset :
        (G.induce G.support).edgeFinset.card ‚â• G.edgeFinset.card
          -c*((Fintype.card G.support)^2-G.support.toFinset.card^2)/2
          -((Fintype.card G.support)-G.support.toFinset.card)/2 by
      convert h_coe_toFinset
      all_goals rw [Set.coe_toFinset]
    simp_rw [Set.toFinset_card G.support, card_edgeFinset_induce_support_eq]
    ring_nf; rfl
  -- inductive step
  . replace hŒ¥ :
        (G.induce G.support).minDegree < c*(Fintype.card G.support) := by
      push_neg at hŒ¥
      convert hŒ¥
      all_goals simp
    have h_card_support_pos : 0 < Fintype.card G.support := by
      contrapose! hŒ¥
      rw [Nat.le_zero] at hŒ¥
      field_simp [hŒ¥]
    haveI : Nonempty G.support := by
      rwa [Fintype.card_pos_iff] at h_card_support_pos
    -- delete minimal degree vertex
    have ‚ü®x, hŒ¥_eq_degx‚ü© := exists_minimal_degree_vertex (G.induce G.support)
    let G' := G.deleteIncidenceSet ‚Üëx
    have ‚ü®s, h_subset, ihŒ¥, ih_card_edgeFinset‚ü© :=
      exists_induced_subgraph_for_minDegree hc_nonneg hc_le_one G'
    replace h_subset : (s : Set V) ‚äÜ G.support \ {‚Üëx} := by
      trans G'.support
      . exact h_subset
      . convert deleteIncidenceSet_support_subset G ‚Üëx
    have hx_not_mem : ‚Üëx ‚àâ (s : Set V) := by
      rw [Set.subset_diff, Set.disjoint_singleton_right] at h_subset
      exact h_subset.2
    -- delete induce eq induce
    replace ihŒ¥ : c*s.card ‚â§ (G.induce s).minDegree := by
      convert ihŒ¥ using 1
      simp_rw [deleteIncidenceSet_induce_of_not_mem G hx_not_mem]
    replace ih_card_edgeFinset :
        (G.induce s).edgeFinset.card ‚â• G'.edgeFinset.card
          - c*((Fintype.card G'.support)^2-s.card^2)/2
          - ((Fintype.card G'.support)-s.card)/2 := by
      convert ih_card_edgeFinset using 1
      simp_rw [Set.toFinset_card,
        deleteIncidenceSet_induce_of_not_mem G hx_not_mem]
    refine ‚ü®s, ?_, ihŒ¥, ?_‚ü©
    . rw [Set.subset_diff] at h_subset
      exact h_subset.1
    . calc ((G.induce s).edgeFinset.card : ‚Ñù)
        _ ‚â• G'.edgeFinset.card
          - (c*((Fintype.card G'.support)^2-s.card^2)/2
            + (Fintype.card G'.support-s.card)/2) := by
            rwa [sub_sub] at ih_card_edgeFinset
        _ ‚â• (G.edgeFinset.card-c*(Fintype.card G.support))
          - (c*((Fintype.card G.support-1)^2-s.card^2)/2
            + (Fintype.card G.support-1-s.card)/2) := by
            apply sub_le_sub
            -- exactly `G.minDegree` edges deleted from edge set
            . rw [card_edgeFinset_deleteIncidenceSet G ‚Üëx,
                Nat.cast_sub (degree_le_card_edgeFinset G x),
                ‚Üêdegree_restrict_support_eq, degree_restrict_eq_induce,
                ‚ÜêhŒ¥_eq_degx]
              exact sub_le_sub_left (le_of_lt hŒ¥) _
            -- at least `x` is deleted from support
            . rw [‚Üêadd_div, ‚Üêadd_div, div_le_div_right zero_lt_two]
              apply add_le_add
              . apply mul_le_mul_of_nonneg_left _ hc_nonneg
                rw [sub_le_sub_iff_right]
                apply pow_le_pow_left (Nat.cast_nonneg _)
                rw [‚ÜêNat.cast_pred Fintype.card_pos, Nat.cast_le]
                exact card_support_deleteIncidenceSet G x.prop
              . rw [sub_le_sub_iff_right,
                  ‚ÜêNat.cast_pred Fintype.card_pos, Nat.cast_le]
                exact card_support_deleteIncidenceSet G x.prop
        _ = (G.edgeFinset.card
            - c*((Fintype.card G.support)^2-s.card^2)/2
            - (Fintype.card G.support-s.card)/2)
          + (1-c)/2 := by ring_nf
        _ ‚â• G.edgeFinset.card
          - c*((Fintype.card G.support)^2-s.card^2)/2
          - (Fintype.card G.support-s.card)/2 := by
            apply le_add_of_nonneg_right
            apply div_nonneg _ zero_le_two
            exact sub_nonneg_of_le hc_le_one
termination_by Fintype.card G.support
decreasing_by
  rw [Nat.pos_iff_ne_zero] at h_card_support_pos
  exact lt_of_le_of_lt
    (card_support_deleteIncidenceSet G x.prop) (Nat.pred_lt h_card_support_pos)

/-- This auxiliary lemma improves the bound on `s.card^2`.

See `exists_induced_subgraph_for_minDegree`. -/
private lemma exists_induced_subgraph_for_minDegree_for_card_sq
    {c Œµ : ‚Ñù} (hc_nonneg : 0 ‚â§ c) (hŒµ_nonneg : 0 ‚â§ Œµ)
    {V : Type*} [Fintype V] [DecidableEq V]
    {G : SimpleGraph V} [DecidableRel G.Adj]
    (h : G.edgeFinset.card ‚â• (c+Œµ)*(Fintype.card V)^2/2) :
    ‚àÉ s : Finset V,
      c*s.card ‚â§ (G.induce s).minDegree ‚àß
      Œµ*(Fintype.card V)^2-(Fintype.card V) ‚â§ s.card^2 := by
  by_cases h_cardV_zero : Fintype.card V = 0
  . use ‚àÖ
    simp [h_cardV_zero]
  . have h_cardV_pos : 0 < Fintype.card V := by
      rwa [‚Üêne_eq, ‚ÜêNat.pos_iff_ne_zero] at h_cardV_zero
    have hc_le_one : c ‚â§ 1 := by
      by_contra! hc_gt_one
      absurd calc ((Fintype.card V)^2/2 : ‚Ñù)
        _ < (c+Œµ)*(Fintype.card V)^2/2 := by
            apply div_lt_div_of_pos_right _ zero_lt_two
            apply lt_mul_of_one_lt_left
            . apply pow_pos
              rw [Nat.cast_pos]
              exact h_cardV_pos
            . apply lt_of_lt_of_le hc_gt_one
              exact le_add_of_nonneg_right hŒµ_nonneg
        _ ‚â§ G.edgeFinset.card := h
      push_neg
      calc (G.edgeFinset.card : ‚Ñù)
        _ ‚â§ (Fintype.card V)*(Fintype.card V-1)/2 := by
            rw [‚ÜêNat.cast_choose_two, Nat.cast_le]
            exact card_edgeFinset_le_card_choose_two
        _ ‚â§ (Fintype.card V)^2/2 := by
            rw [div_le_div_right zero_lt_two, pow_two]
            apply mul_le_mul_of_nonneg_left _ (Nat.cast_nonneg _)
            exact le_of_lt (sub_one_lt _)
    have ‚ü®s, _, hŒ¥, hs‚ü© := exists_induced_subgraph_for_minDegree
      hc_nonneg hc_le_one G
    refine ‚ü®s, hŒ¥, ?_‚ü©
    suffices h_cards_sq :
        Œµ*(Fintype.card V)^2/2-(Fintype.card V)/2 ‚â§ s.card^2/2 by
      rwa [‚Üêsub_div, div_le_div_right zero_lt_two] at h_cards_sq
    calc Œµ*(Fintype.card V)^2/2-(Fintype.card V)/2
      _ = (c+Œµ)*(Fintype.card V)^2/2
        -(c*(Fintype.card V)^2/2 + (Fintype.card V)/2) := by ring_nf
      _ ‚â§ (s.card*(s.card-1)/2
          + (c*((Fintype.card G.support)^2 - s.card^2)/2
          + ((Fintype.card G.support) - s.card)/2))
        - (c*(Fintype.card V)^2/2 + (Fintype.card V)/2) := by
          apply sub_le_sub_right
          apply le_trans h
          rw [ge_iff_le, sub_sub, sub_le_iff_le_add] at hs
          apply le_trans hs
          apply add_le_add_right
          rw [‚ÜêNat.cast_choose_two, Nat.cast_le, ‚ÜêFintype.card_coe s]
          exact card_edgeFinset_le_card_choose_two
      _ = s.card^2/2
        - (c*s.card^2/2 + s.card + c*((Fintype.card V)^2
          - (Fintype.card G.support)^2)/2
          + ((Fintype.card V) - (Fintype.card G.support))/2) := by ring_nf
      _ ‚â§ s.card^2/2 := by
          apply sub_le_self
          repeat apply add_nonneg
          . apply div_nonneg _ zero_le_two
            apply mul_nonneg hc_nonneg
            exact pow_nonneg (Nat.cast_nonneg _) _
          . exact Nat.cast_nonneg _
          . apply div_nonneg _ zero_le_two
            apply mul_nonneg hc_nonneg
            apply sub_nonneg_of_le
            apply pow_le_pow_left (Nat.cast_nonneg _)
            rw [Nat.cast_le]
            exact card_support_le G
          . apply div_nonneg _ zero_le_two
            apply sub_nonneg_of_le
            rw [Nat.cast_le]
            exact card_support_le G

/-- Suppose `r`, `t` are natural numbers and `Œµ > 0` is a real number. A simple
graph of sufficently many vertices `n` having at least `(1-1/r+Œµ)*n^2/2` edges
contains a copy of a complete equipartite graph in `r+1` partitions each of
size `t` as an isomorphic subgraph.

This is the *Erd≈ës-Stone theorem*. -/
theorem isIsoSubgraph_completeEquipartiteGraph_of_card_edgeFinset
    (r t : ‚Ñï) {Œµ : ‚Ñù} (hŒµ_pos : 0 < Œµ) :
    ‚àÉ N, ‚àÄ {V : Type u} [Fintype V] [DecidableEq V], N < Fintype.card V ‚Üí
      ‚àÄ {G : SimpleGraph V} [DecidableRel G.Adj],
        G.edgeFinset.card ‚â• (1-1/r+Œµ)*(Fintype.card V)^2/2
        ‚Üí (completeEquipartiteGraph (Fin (r+1)) (Fin t)).IsIsoSubgraph G := by
  -- choose `Œµ'`
  let Œµ' := Œµ/2
  have hŒµ'_pos : 0 < Œµ' := div_pos hŒµ_pos zero_lt_two
  -- choose `c`
  let c := 1-1/r+Œµ/2
  have hc_pos : 0 < c :=
    add_pos_of_nonneg_of_pos (Nat.one_sub_one_div_cast_nonneg r) hŒµ'_pos
  -- minimal-degree version of Erd≈ës-Stone theorem
  have ‚ü®N, h_iso_sub‚ü© :=
    isIsoSubgraph_completeEquipartiteGraph_of_minDegree hŒµ'_pos r t
  let N' := ‚åà1/Œµ'+N/(sqrt Œµ')‚åâ‚Çä
  use N'
  intro V _ _ hN'_lt_cardV G _ h
  -- `s` satisfies minimal-degree version of Erd≈ës-Stone theorem
  rw [‚Üêadd_halves Œµ, ‚Üêadd_assoc] at h
  obtain ‚ü®s, hŒ¥, h_cards_sq‚ü© :=
    exists_induced_subgraph_for_minDegree_for_card_sq
      (le_of_lt hc_pos) (le_of_lt hŒµ'_pos) h
  -- `x ‚Ü¶ Œµ' * x^2 - x` is strictly monotonic on `[1 / (2 * Œµ'), ‚àû)`
  have h_strictMonoOn :=
    quadratic_strictMonoOn_of_leadingCoeff_pos Œµ' (-1) 0 hŒµ'_pos
  simp_rw [neg_one_mul, ‚Üêsub_eq_add_neg, add_zero, neg_neg] at h_strictMonoOn
  have hN_mem_Ici : (1/Œµ'+N/sqrt Œµ' : ‚Ñù) ‚àà Set.Ici (1/(2*Œµ')) := by
    rw [Set.mem_Ici]
    trans 1/Œµ'
    . rw [mul_comm, ‚Üêdiv_div, half_le_self_iff, one_div_nonneg]
      exact le_of_lt hŒµ'_pos
    . apply le_add_of_nonneg_right
      exact div_nonneg (Nat.cast_nonneg _) (sqrt_nonneg Œµ')
  have hN'_mem_Ici : (‚åà1/Œµ'+N/sqrt Œµ'‚åâ‚Çä : ‚Ñù) ‚àà Set.Ici (1/(2*Œµ')) := by
    rw [Set.mem_Ici]
    trans 1/Œµ'+N/sqrt Œµ'
    . rwa [Set.mem_Ici] at hN_mem_Ici
    . exact Nat.le_ceil _
  have h_cardV_mem_Ici : (Fintype.card V : ‚Ñù) ‚àà Set.Ici (1/(2*Œµ')) := by
    rw [Set.mem_Ici]
    trans (N' : ‚Ñù)
    . rwa [Set.mem_Ici] at hN'_mem_Ici
    . rw [Nat.cast_le]
      exact le_of_lt hN'_lt_cardV
  -- `s.card` satisfies minimal-degree version of Erd≈ës-Stone theorem
  have hN_sq_lt_cards_sq : (N^2 : ‚Ñù) < (s.card^2 : ‚Ñù) :=
    calc (s.card^2 : ‚Ñù)
      _ ‚â• Œµ'*(Fintype.card V)^2-(Fintype.card V) := h_cards_sq
      _ > Œµ'*N'^2-N' := by
          apply h_strictMonoOn hN'_mem_Ici h_cardV_mem_Ici
          rw [Nat.cast_lt]
          exact hN'_lt_cardV
      _ ‚â• Œµ'*(1/Œµ'+N/(sqrt Œµ'))^2-(1/Œµ'+N/(sqrt Œµ')) := by
          apply h_strictMonoOn.monotoneOn hN_mem_Ici hN'_mem_Ici
          exact Nat.le_ceil _
      _ = N^2+N/sqrt Œµ' := by
          ring_nf
          field_simp
          ring_nf
      _ ‚â• N^2 := by
          apply le_add_of_nonneg_right
          exact div_nonneg (Nat.cast_nonneg _) (sqrt_nonneg Œµ')
  have hN_lt_cards : N < s.card := by
    rwa [‚ÜêNat.cast_pow, ‚ÜêNat.cast_pow, Nat.cast_lt,
      Nat.pow_lt_pow_iff_left two_ne_zero] at hN_sq_lt_cards_sq
  -- identify `completeEquipartiteGraph (Fin (r+1)) (Fin t')`
  simp_rw [‚ÜêFintype.card_coe, ‚ÜêFinset.coe_sort_coe] at hN_lt_cards hŒ¥
  exact (h_iso_sub hN_lt_cards hŒ¥).trans ‚ü®SubgraphIso.induce G s‚ü©

variable {W : Type*} [Fintype W] {H : SimpleGraph W}

omit [Fintype W] in
lemma chromaticNumber_succ_iff {r : ‚Ñï} :
    H.chromaticNumber = r+1 ‚Üî ¬¨H.Colorable r ‚àß H.Colorable (r+1) := by
  constructor
  . intro h
    constructor
    . contrapose! h
      apply ne_of_lt
      rw [‚ÜêchromaticNumber_le_iff_colorable] at h
      apply lt_of_le_of_lt h
      rw [‚ÜêNat.cast_add_one, Nat.cast_lt]
      exact Nat.lt_succ_self _
    . rw [‚ÜêchromaticNumber_le_iff_colorable, le_iff_eq_or_lt]
      left; exact h
  . intro ‚ü®nh, h‚ü©
    apply le_antisymm
    . rwa [‚ÜêchromaticNumber_le_iff_colorable, Nat.cast_add] at h
    . rw [‚ÜêchromaticNumber_le_iff_colorable.not] at nh
      push_neg at nh
      exact Order.add_one_le_of_lt nh

/-- Suppose `r > 0` is a natural number such that the chromatic number of the
simple graph `H` equals `r+1`, and `Œµ > 0` is a real number. The extremal
number `extremalNumber V H` for a sufficently large vertex type `V` is greater
than `(1-1/r-Œµ)*(card V)^2/2` and at most `(1-1/r+Œµ)*(card V)^2/2`.

This is the *Erd≈ës-Stone-Simonovits theorem*. -/
theorem lt_extremalNumber_le_of_chromaticNumber {Œµ : ‚Ñù} (hŒµ_pos : 0 < Œµ)
    {r : ‚Ñï} (hr_pos : 0 < r) (hœá : H.chromaticNumber = r+1) :
    ‚àÉ N, ‚àÄ {V : Type u} [Fintype V] [DecidableEq V], N < Fintype.card V ‚Üí
      (1-1/r-Œµ)*(Fintype.card V)^2/2 < extremalNumber V H ‚àß
      extremalNumber V H ‚â§ (1-1/r+Œµ)*(Fintype.card V)^2/2 := by
  have ‚ü®nhc, hc‚ü© := chromaticNumber_succ_iff.mp hœá
  have ‚ü®N‚ÇÅ, h‚ÇÅ‚ü© := lt_ex nhc
  have ‚ü®N‚ÇÇ, h‚ÇÇ‚ü© := ex_le hc
  use max N‚ÇÅ N‚ÇÇ
  intro V _ _ h_cardV
  refine ‚ü®h‚ÇÅ ?_, h‚ÇÇ ?_‚ü©
  . exact lt_of_le_of_lt (Nat.le_max_left N‚ÇÅ N‚ÇÇ) h_cardV
  . exact lt_of_le_of_lt (Nat.le_max_right N‚ÇÅ N‚ÇÇ) h_cardV
where
  lt_ex (nhc : ¬¨H.Colorable r) :
      ‚àÉ N, ‚àÄ {V : Type u} [Fintype V] [DecidableEq V], N < Fintype.card V ‚Üí
        (1-1/r-Œµ)*(Fintype.card V)^2/2 < extremalNumber V H := by
    let N := ‚åà2*r/Œµ‚åâ‚Çä
    use N
    intro V _ _ hN_lt_cardV
    have h_cardV_pos : 0 < Fintype.card V :=
      lt_of_le_of_lt N.zero_le hN_lt_cardV
    by_cases hlt_Œµ : 1-1/r < Œµ
    . calc (1-1/r-Œµ)*(Fintype.card V)^2/2
        _ < 0 := by
            rw [‚Üêsub_neg] at hlt_Œµ
            apply div_neg_of_neg_of_pos _ zero_lt_two
            apply mul_neg_of_neg_of_pos hlt_Œµ
            apply pow_pos
            rw [Nat.cast_pos]
            exact lt_of_le_of_lt (Nat.zero_le _) hN_lt_cardV
        _ ‚â§ extremalNumber V H := Nat.cast_nonneg _
    . have hŒµ_le : Œµ ‚â§ 1-1/r := le_of_not_lt hlt_Œµ
      have hr_lt_cardV : (r : ‚Ñù) < (Fintype.card V : ‚Ñù) := by
        calc (r : ‚Ñù)
          _ ‚â§ 2*r/Œµ := by
              rw [le_div_iff‚ÇÄ hŒµ_pos, mul_comm, mul_le_mul_right]
              exact hŒµ_le.trans
                ((Nat.one_sub_one_div_cast_le_one r).trans one_le_two)
              rw [Nat.cast_pos]
              exact hr_pos
          _ ‚â§ N := Nat.le_ceil _
          _ < Fintype.card V := by
              rw [Nat.cast_lt]
              exact hN_lt_cardV
      replace hr_lt_cardV : r < Fintype.card V := by
          rwa [Nat.cast_lt] at hr_lt_cardV
      have h2r_lt_ŒµcardV : 2*r < Œµ*(Fintype.card V) := by
        rw [‚Üêdiv_lt_iff' hŒµ_pos]
        apply lt_of_le_of_lt (Nat.le_ceil _)
        rw [Nat.cast_lt]
        exact hN_lt_cardV
      -- `completeEquipartiteGraph (Fin r) (Fin t)` does not contain `H`
      let t := Fintype.card V/r
      haveI : Nonempty (Fin r √ó Fin t) := by
        simp_rw [nonempty_prod, ‚ÜêFin.pos_iff_nonempty]
        exact ‚ü®hr_pos, Nat.div_pos (le_of_lt hr_lt_cardV) hr_pos‚ü©
      have hc : (completeEquipartiteGraph (Fin r) (Fin t)).Colorable r :=
        completeEquipartiteGraph_colorable_of_fin
      apply lt_extremalNumber_of_colorable _ nhc hc _
      . simp_rw [Fintype.card_prod, Fintype.card_fin]
        exact Nat.mul_div_le _ _
      . calc (1-1/r-Œµ)*(Fintype.card V)^2/2
          _ < (1-1/r)*(Fintype.card V)^2/2-r*(Fintype.card V) := by
              rw [sub_mul, sub_div]
              apply sub_lt_sub_left
              rw [lt_div_iff zero_lt_two, mul_comm, ‚Üêmul_assoc, pow_two,
                ‚Üêmul_assoc]
              apply mul_lt_mul_of_pos_right
              exact h2r_lt_ŒµcardV
              rw [Nat.cast_pos]
              exact h_cardV_pos
          _ = (1-1/‚Üër)*‚Üër^2*t^2/2
            - (‚Üër*‚Üë(Fintype.card V)
              - (1-1/‚Üër)*(‚Üë(Fintype.card V)*‚Üë(Fintype.card V%r)))
            - (1-1/‚Üër)*‚Üë(Fintype.card V % r)^2/2 := by
              conv_lhs =>
                lhs; rw [‚ÜêNat.div_add_mod (Fintype.card V) r, Nat.cast_add,
                  add_sq, add_assoc, mul_add, add_div]
                lhs; rw [Nat.cast_mul, mul_pow, ‚Üêmul_assoc]
              rw [Nat.mul_div_eq_sub_mod (Fintype.card V) r,
                Nat.cast_sub (Nat.mod_le _ _)]
              ring_nf
          _ ‚â§ (1-1/r)*r^2*t^2/2 := by
              rw [sub_sub]
              apply sub_le_self
              apply add_nonneg
              . apply sub_nonneg_of_le
                rw [‚Üêmul_assoc, mul_comm (r : ‚Ñù) _]
                apply mul_le_mul _ _ (Nat.cast_nonneg _) (Nat.cast_nonneg _)
                . rw [mul_le_iff_le_one_left]
                  exact Nat.one_sub_one_div_cast_le_one r
                  rw [Nat.cast_pos]
                  exact h_cardV_pos
                . rw [Nat.cast_le]
                  exact le_of_lt (Nat.mod_lt _ hr_pos)
              . apply div_nonneg _ zero_le_two
                apply mul_nonneg (Nat.one_sub_one_div_cast_nonneg _)
                rw [‚ÜêNat.cast_pow]
                exact Nat.cast_nonneg _
          _ = (completeEquipartiteGraph (Fin r) (Fin t)).edgeFinset.card := by
              simp_rw [card_edgeFinset_completeEquipartiteGraph,
                Fintype.card_fin, Nat.cast_mul, Nat.cast_pow,
                Nat.cast_choose_two]
              field_simp
              ring_nf
  ex_le (hc : H.Colorable (r+1)) :
      ‚àÉ N, ‚àÄ {V : Type u} [Fintype V] [DecidableEq V], N < Fintype.card V ‚Üí
        extremalNumber V H ‚â§ (1-1/r+Œµ)*(Fintype.card V)^2/2 := by
    have ‚ü®t, h‚ÇÅ‚ü© :=
      isIsoSubgraph_completeEquipartiteGraph_of_colorable hc
    -- Erd≈ës-Stone theorem
    have ‚ü®N, h‚ÇÇ‚ü© :=
      isIsoSubgraph_completeEquipartiteGraph_of_card_edgeFinset r t hŒµ_pos
    use N
    intro V _ _ hN_lt_cardV
    trans (extremalNumber V (completeEquipartiteGraph (Fin (r+1)) (Fin t)) : ‚Ñù)
    -- `completeEquipartiteGraph (Fin (r+1)) (Fin t)` contains `H`
    . rw [Nat.cast_le]
      apply extremalNumber_le_extremalNumber_of_isIsoSubgraph
      apply h‚ÇÅ
      rw [Fintype.card_fin]
    . have h_nonneg : 0 ‚â§ (1-1/r+Œµ)*(Fintype.card V)^2/2 := by
        apply div_nonneg _ zero_le_two
        apply mul_nonneg
        . apply add_nonneg _ (le_of_lt hŒµ_pos)
          exact Nat.one_sub_one_div_cast_nonneg _
        . rw [‚ÜêNat.cast_pow]
          exact Nat.cast_nonneg _
      -- contains `completeEquipartiteGraph (Fin (r+1)) (Fin t)`
      rw [extremalNumber_le_iff_of_nonneg V _ h_nonneg]
      classical
      intro G
      contrapose!
      intro h
      exact h‚ÇÇ hN_lt_cardV (le_of_lt h)

/-- Suppose `r > 0` is a natural number such that the chromatic number of the
simple graph `H` equals `r+1`. The difference of the extremal number
`extremalNumber (Fin n) H` and `(1-1/r)*n^2/2` is little-o of `n^2` at
infinity.

This is a corollary of the *Erd≈ës-Stone-Simonovits theorem*. -/
theorem isLittleO_extremalNumber_of_chromaticNumber
    {r : ‚Ñï} (hr_pos : 0 < r) (hœá : H.chromaticNumber = r+1) :
    (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H-(1-1/r)*n^2/2 : ‚Ñù))
      =o[Filter.atTop] (fun (n : ‚Ñï) ‚Ü¶ (n^2 : ‚Ñù)) := by
  rw [isLittleO_iff]
  intro Œµ hŒµ_pos
  rw [Filter.eventually_atTop]
  have ‚ü®N, h‚ü© := lt_extremalNumber_le_of_chromaticNumber hŒµ_pos hr_pos hœá
  use N+1
  intro n hn
  replace hn : Fintype.card (Fin n) > N := by
    rw [Fintype.card_fin]
    exact hn
  specialize h hn
  rw [Fintype.card_fin] at h
  rw [norm_eq_abs, ‚Üêabs_of_pos hŒµ_pos, norm_eq_abs,
    ‚Üêabs_mul, ‚Üêsq_le_sq]
  apply sq_le_sq'
  all_goals linarith

/-- Suppose `r > 0` is a natural number such that the chromatic number of the
simple graph `H` equals `r+1`. The density of the extremal graphs that do not
contain `H` as an isomorphic subgraph tends to `1-1/r` at infinity.

This is a corollary of the *Erd≈ës-Stone-Simonovits theorem*. -/
theorem tendsto_extremalNumber_div_choose_two_of_chromaticNumber
    {r : ‚Ñï} (hr_pos : 0 < r) (hœá : H.chromaticNumber = r+1) :
    Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H / n.choose 2 : ‚Ñù))
      Filter.atTop (ùìù (1-1/r)) := by
  have h‚ÇÅ : Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ (1-(n/(n-1)) : ‚Ñù))
      Filter.atTop (ùìù 0) := by
    rw [‚Üêsub_self 1]
    exact (tendsto_const_nhds).sub (tendsto_natCast_div_add_atTop (-1 : ‚Ñù))
  apply Filter.Tendsto.const_mul (1-1/r : ‚Ñù) at h‚ÇÅ
  simp_rw [mul_zero, mul_sub, mul_one] at h‚ÇÅ
  have h‚ÇÇ := IsLittleO.trans_isTheta
    (isLittleO_extremalNumber_of_chromaticNumber hr_pos hœá)
    Nat.isTheta_choose_two.symm
  apply IsLittleO.tendsto_div_nhds_zero at h‚ÇÇ
  simp_rw [sub_div, mul_div_assoc] at h‚ÇÇ
  have h‚ÇÇ_sub_h‚ÇÅ := h‚ÇÇ.sub h‚ÇÅ
  simp_rw [sub_zero] at h‚ÇÇ_sub_h‚ÇÅ
  rw [‚Üêtendsto_sub_nhds_zero_iff]
  convert h‚ÇÇ_sub_h‚ÇÅ using 1
  ext n
  by_cases hn : (n : ‚Ñù) = 0
  . field_simp [hn]
  . rw [Nat.cast_choose_two, div_div_div_eq, pow_two,
      ‚Üêmul_assoc 2 _ (n-1 : ‚Ñù), mul_assoc (n : ‚Ñù) _ 2, mul_comm (n : ‚Ñù) 2,
      mul_comm (n : ‚Ñù) (2*n : ‚Ñù), mul_div_mul_comm]
    field_simp [hn]
    ring_nf

/-- Suppose `r > 0` is a natural number such that the chromatic number of the
simple graph `H` equals `r+1`. The Tur√°n density of `H` equals `1-1/r`.

This is a corollary of the *Erd≈ës-Stone-Simonovits theorem*. -/
theorem turanDensity_eq_of_chromaticNumber
    {r : ‚Ñï} (hr_pos : 0 < r) (hœá : H.chromaticNumber = r+1) :
    turanDensity H = 1-1/r :=
  Filter.Tendsto.limUnder_eq
    (tendsto_extremalNumber_div_choose_two_of_chromaticNumber hr_pos hœá)

/-- Suppose `r > 1` is a natural number such that the chromatic number of the
simple graph `H` equals `r+1`. The extremal number `extremalNumber (Fin n) H`
is asymptotically equivalent to `(1-1/r)*(n.choose 2)` at infinity.

This is a corollary of the *Erd≈ës-Stone-Simonovits theorem*. -/
theorem isEquivalent_extremalNumber_of_chromaticNumber
    {r : ‚Ñï} (hr : 1 < r) (hœá : H.chromaticNumber = r+1) :
    (fun (n : ‚Ñï) ‚Ü¶ (extremalNumber (Fin n) H : ‚Ñù))
      ~[Filter.atTop] (fun (n : ‚Ñï) ‚Ü¶ ((1-1/r)*(n.choose 2) : ‚Ñù)) := by
  have hœÄ : turanDensity H = 1-1/r :=
    turanDensity_eq_of_chromaticNumber (hr.trans' zero_lt_one) hœá
  have hœÄ_pos : 0 < turanDensity H := by
    rw [hœÄ]
    exact Nat.one_sub_one_div_cast_pos hr
  have h := isEquivalent_extremalNumber H hœÄ_pos.ne'
  simpa [hœÄ] using h
